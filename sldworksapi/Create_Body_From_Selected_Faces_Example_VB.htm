<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Create Body From Selected Faces Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2156>
<meta name=layout-width content=723>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">
 <META NAME="MS-HKWD" CONTENT="VBA examples,bodies">
 <META NAME="MS-HKWD" CONTENT="VBA examples,faces">
  <META NAME="MS-HKWD" CONTENT="VBA examples,temporary body">
</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Create Body From Selected Faces Example (VBA)</h1>

<p>This example shows how to:</p>
<ul>
	<li><font face="Verdana" style="font-size: 8pt">use SOLIDWORKS geometry and 
	topology methods to construct a temporary body from selected faces.</font></li>
	<li><font face="Verdana" style="font-size: 8pt">create a solid body feature 
	from the temporary body and a new part containing the solid body feature.</font></li>
</ul>
<pre>'------------------------------------------
' Preconditions:
' 1. Verify that the specified part document
'    template exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens a new part document and creates an
'    extruded thin feature.
' 2. Selects connecting faces on the extruded thin feature.
' 3. Knits the faces into a solid, creates a
'    a new part, and inserts the solid as an imported
'    solid body feature.
' 4. Examine the Immediate window, graphics area, 
'    FeatureManager design tree, and document name 
'    in the SOLIDWORKS menu bar.
'-------------------------------------------
Option Explicit</pre>
<pre>Sub main()</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swModelDocExt As SldWorks.ModelDocExtension
    Dim swSketchManager As SldWorks.SketchManager
    Dim swSketchSegment As SldWorks.SketchSegment
    Dim swFeatureManager As SldWorks.FeatureManager
    Dim swFeature As SldWorks.Feature
    Dim swPart As SldWorks.PartDoc
    Dim swNewPart As SldWorks.PartDoc
    Dim swModeler As SldWorks.Modeler
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swSelFace() As SldWorks.Face2
    Dim vFaceList As Variant
    Dim swBody As SldWorks.Body2
    Dim swNewBody As SldWorks.Body2
    Dim swFeat As SldWorks.Feature
    Dim nSelCount As Long
    Dim i As Long
    Dim bRet As Boolean
</pre>
<pre>    Set swApp = CreateObject(&quot;SldWorks.Application&quot;)    </pre>
<pre>    'Create part and select connecting faces
    Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SolidWorks 2015\templates\Part.prtdot&quot;, 0, 0, 0)
    Set swSketchManager = swModel.<b>SketchManager</b>
    swSketchManager.<b>InsertSketch</b> True
    Set swSketchSegment = swSketchManager.<b>CreateLine</b>(0#, 0#, 0#, -0.062359, 0#, 0#)
    Set swSketchSegment = swSketchManager.<b>CreateLine</b>(-0.062359, 0#, 0#, -0.020485, 0.066264, 0#)
    Set swSketchSegment = swSketchManager.<b>CreateLine</b>(-0.020485, 0.066264, 0#, 0#, 0#, 0#)
    swModel.<b>ClearSelection2</b> True
    Set swFeatureManager = swModel.<b>FeatureManager</b>
    Set swFeature = swFeatureManager.<b>FeatureExtrusionThin2</b>(True, False, False, 0, 0, 0.03048, 0.00254, False, False, False, False, 1.74532925199433E-02, 1.74532925199433E-02, False, False, False, False, True, 0.00254, 0.00254, 0.00254, 0, 0, False, 0.005, True, True, 0, 0, False)
    Set swSelMgr = swModel.<b>SelectionManager</b>
    swSelMgr.<b>EnableContourSelection</b> = False
    swModel.<b>ClearSelection2</b> True
    Set swModelDocExt = swModel.<b>Extension</b>
    bRet = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, -4.84137409629284E-02, 0, 0.018103012393226, True, 0, Nothing, 0)
    bRet = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, -3.96839112014504E-02, 0.035882458904041, 2.07108765403632E-02, True, 0, Nothing, 0)
    bRet = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, -1.75462018075336E-02, 5.67577015655729E-02, 0.021527257415471, True, 0, Nothing, 0)</pre>
<pre>    'Get the selected faces
    Set swModeler = swApp.<b>GetModeler</b>
    nSelCount = swSelMgr.<b>GetSelectedObjectCount</b>
    ReDim swSelFace(nSelCount - 1)
    For i = 1 To nSelCount
        Set swSelFace(i - 1) = swSelMgr.<b>GetSelectedObject6</b>(i, -1)
    Next
    vFaceList = swSelFace
    </pre>
<pre>    'Create solid body feature using selected faces
    Set swPart = swModel
    Set swBody = swPart.<b>CreateNewBody</b>
    Set swNewBody = swBody.<b>CreateBodyFromFaces</b>(nSelCount, (vFaceList))
    If swNewBody Is Nothing Then
        Debug.Print &quot;Failed to create solid body from selected faces.&quot;
        Exit Sub
    Else
        Debug.Print &quot;Solid body and new part can be created from selected faces.&quot;
    End If</pre>
<pre>    'Open new part and force creation of solid body feature
    Set swNewPart = swApp.<b>NewPart</b>
    Set swFeat = swNewPart.<b>CreateFeatureFromBody3</b>(swNewBody, False, swCreateFeatureBodyOpts_e.swCreateFeatureBodyCheck + swCreateFeatureBodyOpts_e.swCreateFeatureBodySimplify)
    If Not swFeat Is Nothing Then
        Debug.Print &quot;New part with imported solid body created.&quot;
    Else
        Debug.Print &quot;New part with imported solid body not created.&quot;
    End If</pre>
<pre>End Sub
</pre>

</body>

</html>
