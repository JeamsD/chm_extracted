<html>

<head>
<title>Get Guide Curves in Loft Feature Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,lofts">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Get Guide Curves in Loft Feature Example (VBA)</h1>
<p>This example shows how to get the guide curves in a loft feature.</p>

<pre>'----------------------------------------
' Preconditions:
' 1. Verify that the specified part document
'    template exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Creates a new part document.
' 2. Creates a loft feature.
' 3. Prints to the Immediate window
'    the feature type and feature name of the loft 
'    feature.
' 4. Accesses the guide curves in the loft feature.
' 5. Prints to the Immediate window whether the
'    loft is a boss feature, the number guide
'    curves in the loft, and the feature types 
'    of the guide curves.
' 6. Releases access to the loft feature.
' 7. Examine the Immediate window.
'----------------------------------------</pre>
<pre>Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swFeatureManager As SldWorks.FeatureManager
Dim swRefPlane As SldWorks.RefPlane
Dim swSketchManager As SldWorks.SketchManager
Dim swSketchSegment As SldWorks.SketchSegment
Dim swFeature As SldWorks.Feature
Dim swLoftFeatureData As SldWorks.LoftFeatureData
Dim pointArray As Variant
Dim points() As Double
Dim guideCurves As Variant
Dim guideCurve As Object
Dim nbrGuideCurves As Long
Dim i As Long
Dim status As Boolean</pre>
<pre>Sub main()</pre>
<pre>    'Open new part document
    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SolidWorks 2015\templates\Part.prtdot&quot;, 0, 0, 0)
    Set swModelDocExt = swModel.<b>Extension</b>    </pre>
<pre>    'Create reference plane
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Right Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, True, 0, Nothing, 0)
    Set swFeatureManager = swModel.<b>FeatureManager</b>
    Set swRefPlane = swFeatureManager.<b>InsertRefPlane</b>(8, 0.0635, 0, 0, 0, 0)
    swModel.<b>ClearSelection2</b> True    </pre>
<pre>    'Create circle for loft feature
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Plane1&quot;, &quot;PLANE&quot;, 0, 0, 0, True, 0, Nothing, 0)
    Set swSketchManager = swModel.<b>SketchManager</b>
    swSketchManager.<b>InsertSketch</b> True
    Set swSketchSegment = swSketchManager.<b>CreateCircle</b>(-0#, 0#, 0#, 0.003857, -0.009744, 0#)
    swModel.<b>ClearSelection2</b> True
    swSketchManager.<b>InsertSketch</b> True    </pre>
<pre>    'Create another circle for loft feature
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Right Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    swSketchManager.<b>InsertSketch</b> True
    Set swSketchSegment = swSketchManager.<b>CreateCircle</b>(-0#, 0#, 0#, 0.014007, -0.029232, 0#)
    swModel.<b>ClearSelection2</b> True
    swSketchManager.<b>InsertSketch</b> True    </pre>
<pre>    'Create sketch for guide curve
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Front Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    swModel.<b>ClearSelection2</b> True
    ReDim points(0 To 8) As Double
    points(0) = 0
    points(1) = 3.24150959148675E-02
    points(2) = 0
    points(3) = 0.02176137524458
    points(4) = 2.09549481725162E-02
    points(5) = 0
    points(6) = 0.0635
    points(7) = 1.04797609372824E-02
    points(8) = 0
    pointArray = points
    swSketchManager.<b>InsertSketch</b> True
    Set swSketchSegment = swSketchManager.<b>CreateSpline</b>((pointArray))
    swSketchManager.<b>InsertSketch</b> True    </pre>
<pre>    'Create loft feature with guide curve
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCH&quot;, 0, 0, 0, True, 0, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch2&quot;, &quot;SKETCH&quot;, 0, 0, 0, True, 0, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch3&quot;, &quot;SKETCH&quot;, 0, 0, 0, True, 0, Nothing, 0)
    swModel.<b>ClearSelection2</b> True
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCH&quot;, 0.0635, 0, -1.04797609372824E-02, False, 1, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch2&quot;, &quot;SKETCH&quot;, 0, 0, -3.24150959148675E-02, True, 1, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch3&quot;, &quot;SKETCH&quot;, 0, 0, 0, True, 4098, Nothing, 0)
    Set swFeature = swFeatureManager.<b>InsertProtrusionBlend2</b>(False, True, False, 1, 0, 0, 1, 1, True, True, False, 0, 0, 0, True, True, True, swGuideCurveInfluence_e.swGuideCurveInfluenceNextGlobal)
    Debug.Print &quot;Feature:&quot;
    Debug.Print &quot;  Type: &quot; &amp; swFeature.<b>GetTypeName2</b>
    Debug.Print &quot;  Name: &quot; &amp; swFeature.<b>Name</b>    </pre>
<pre>    'Change the orientation of the view
    swModel.<b>ShowNamedView2</b> &quot;*Isometric&quot;, 7    </pre>
<pre>    'Access loft feature data, get guide curves,
    'get feature type of guide curves, and release
    'access to loft feature
    Set swLoftFeatureData = swFeature.<b>GetDefinition</b>
    Debug.Print (&quot;   Boss feature: &quot; &amp; swLoftFeatureData.<b>IsBossFeature</b>)
    nbrGuideCurves = swLoftFeatureData.<b>GetGuideCurvesCount</b>
    Debug.Print (&quot;   Number of guide curves: &quot; &amp; nbrGuideCurves)
    status = swLoftFeatureData.<b>AccessSelections</b>(swModel, Nothing)
    Debug.Print (&quot;    Guide curve: &quot;)
    guideCurves = swLoftFeatureData.<b>guideCurves</b>
    For i = 0 To (nbrGuideCurves - 1)
      Set guideCurve = guideCurves(i)
      Debug.Print (&quot;        Type of feature: &quot; &amp; swLoftFeatureData.<b>GetGuideCurvesType</b>(i))
    Next i
    swLoftFeatureData.<b>ReleaseSelectionAccess</b>    </pre>
<pre>End Sub</pre>

</body>

</html>
