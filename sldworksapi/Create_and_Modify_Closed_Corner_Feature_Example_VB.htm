<html>

<head>
<title>Create and Modify Closed Corner Feature Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,sheet metal">
<meta name=MS-HKWD content="VBA examples,features">
<meta name=MS-HKWD content="VBA examples,closed corner">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Create and Modify Closed Corner Feature Example (VBA)</h1>
<p>This example shows how to create and modify a closed corner feature in a sheet metal part.</p>

<pre>'------------------------------------------------------------------
' Preconditions:
' 1. Verify that the specified sheet metal part document to
'    open exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the specified sheet metal part document.
' 2. Selects two faces.
' 3. Inserts a closed corner feature.
' 4. Modifies properties of the closed corner feature.
' 5. Examine the graphics area and Immediate window.
'
' NOTE: Because this part document is used elsewhere,
' do not save changes.
'--------------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swFeature As SldWorks.Feature
Dim swSelectionMgr As SldWorks.SelectionMgr
Dim swClosedCornerFeatureData As SldWorks.ClosedCornerFeatureData
Dim status As Boolean
Dim errors As Long
Dim warnings As Long
Dim fileName As String
Dim faces As Variant
Dim swFace As SldWorks.Face2
Dim i As Long
</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\sheetmetal\formtools\cover.sldprt&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocPART, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swModelDocExt = swModel.<b>Extension</b>
    Set swSelectionMgr = swModel.<b>SelectionManager</b>    </pre>
<pre>    'Select the faces for the closed corner feature
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, 1.10595835492404E-02, 2.80018934407167E-02, 4.97348782814129E-02, True, 1, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, 1.81424245698736E-02, 1.10595835492404E-02, 4.95671179450028E-02, True, 1073741824, Nothing, 0)    </pre>
<pre>    'Insert the closed corner feature
    swModel.<b>InsertSheetMetalClosedCorner</b>    </pre>
<pre>    'Select the closed corner feature
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Closed Corner1&quot;, &quot;BODYFEATURE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swFeature = swSelectionMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swClosedCornerFeatureData = swFeature.<b>GetDefinition</b>    </pre>
<pre>    'Access closed corner feature
    status = swClosedCornerFeatureData.<b>AccessSelections</b>(swModel, Nothing)
        Debug.Print (&quot;Original properties: &quot;)
        Debug.Print (&quot;  Corner type: &quot; &amp; swClosedCornerFeatureData.<b>CornerType</b>)
        Debug.Print (&quot;  Gap distance: &quot; &amp; swClosedCornerFeatureData.<b>GapDistance</b>)
        Debug.Print (&quot;  Open bend region? &quot; &amp; swClosedCornerFeatureData.<b>OpenBendRegion</b>)
        Debug.Print (&quot;  Overlap/underlap ratio: &quot; &amp; swClosedCornerFeatureData.<b>OverlapUnderlapRatio</b>)
        faces = swClosedCornerFeatureData.<b>faces</b>
        For i = 0 To UBound(faces)
            Set swFace = faces(i)
            Debug.Print (&quot;  Area of face &quot; &amp; i &amp; &quot;: &quot; &amp; swFace.<b>GetArea</b> * 1000 &amp; &quot; mm&quot;)
        Next i        </pre>
<pre>        Debug.Print (&quot;Modified properties: &quot;)
        swClosedCornerFeatureData.<b>CornerType</b> = swClosedCornerTypes_e.swClosedCornerTypeUnderlap
        swClosedCornerFeatureData.<b>GapDistance</b> = 0.005
        swClosedCornerFeatureData.<b>OpenBendRegion</b> = True
        swClosedCornerFeatureData.<b>OverlapUnderlapRatio</b> = 0.5
        Debug.Print (&quot;  Corner type: &quot; &amp; swClosedCornerFeatureData.<b>CornerType</b>)
        Debug.Print (&quot;  Gap distance: &quot; &amp; swClosedCornerFeatureData.<b>GapDistance</b>)
        Debug.Print (&quot;  Open bend region? &quot; &amp; swClosedCornerFeatureData.<b>OpenBendRegion</b>)
        Debug.Print (&quot;  Overlap/underlap ratio: &quot; &amp; swClosedCornerFeatureData.<b>OverlapUnderlapRatio</b>)        </pre>
<pre>        status = swFeature.<b>ModifyDefinition</b>(swClosedCornerFeatureData, swModel, Nothing)</pre>
<pre>End Sub</pre>

</body>

</html>
