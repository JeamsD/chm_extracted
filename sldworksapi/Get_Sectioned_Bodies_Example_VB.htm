<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Get Sectioned Bodies Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=1191>
<meta name=layout-width content=628>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">
 <META NAME="MS-HKWD" CONTENT="VBA examples,bodies">
 <META NAME="MS-HKWD" CONTENT="VBA examples,sectioned bodies">
</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Get Sectioned Bodies Example (VBA)</h1>

<p>This example shows how to get sectioned bodies and create a new part 
 using the sectioned bodies.</p>

<pre>'-------------------------------------------------------
' Preconditions:
' 1. Verify that the specified assembly document to open exists.
' 2. Verify that the specified part document template exists.
' 3. Open the Immediate window.
' 4. Step through the macro by pressing F8.
'
' Postconditions:
' 1. Opens the specified assembly document.
' 2. Creates a section view.
' 3. Selects a component in the section view.
' 4. Creates a new part.
' 5. Creates a feature using the body in the component
'    in the section view selected in step 3.
' 6. Examine the FeatureManager design tree, graphics
'    area, and Immediate window.
'
' NOTE: Because the assembly is used elsewhere, do not
' save changes.
'--------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swModelViewMgr As SldWorks.ModelViewManager
Dim swSelMgr As SldWorks.SelectionMgr
Dim swComp As SldWorks.Component2
Dim swModelView As SldWorks.ModelView
Dim swSectionViewData As SldWorks.SectionViewData
Dim vBodyArr As Variant
Dim vBody As Variant
Dim swBody As SldWorks.Body2
Dim swNewPart As SldWorks.PartDoc
Dim swFeat As SldWorks.Feature
Dim status As Boolean
Dim errors As Long
Dim warnings As Long
Dim fileName As String</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks</pre>
<pre>    'Open assembly,create section view, and select
    'a component in the section view
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\landing_gear.sldasm&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocASSEMBLY, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swModelDocExt = swModel.<b>Extension</b>
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Plane1&quot;, &quot;PLANE&quot;, 0, 0, 0, True, 1, Nothing, 0)
    Set swModelViewMgr = swModel.<b>ModelViewManager</b>
    Set swSectionViewData = swModelViewMgr.<b>CreateSectionViewData</b>()
    swSectionViewData.<b>FirstPlane</b> = Nothing
    swSectionViewData.<b>FirstReverseDirection</b> = False
    swSectionViewData.<b>FirstOffset</b> = 0
    swSectionViewData.<b>FirstRotationX</b> = 0
    swSectionViewData.<b>FirstRotationY</b> = 0
    swSectionViewData.<b>FirstColor</b> = 16711680
    swSectionViewData.<b>ShowSectionCap</b> = True
    swSectionViewData.<b>KeepCapColor</b> = False
    swSectionViewData.<b>GraphicsOnlySection</b> = False
    status = swModelViewMgr.<b>CreateSectionView</b>(swSectionViewData)
    swModel.<b>ClearSelection2</b> True
    status = swModelDocExt.<b>SelectByID2</b>(&quot;oleostrut-1@landing_gear&quot;, &quot;COMPONENT&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swComp = swSelMgr.<b>GetSelectedObjectsComponent4</b>(1, -1)</pre>
<pre>
    Set swModelView = swModel.<b>ActiveView</b>
    status = swModelView.<b>GetDisplayState</b>(swViewDisplayType_e.swIsViewSectioned)
    If status Then
         'Create new part using selected component
         Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SolidWorks 2014\templates\part.prtdot&quot;, swDwgPaperAsize, 0, 0)
         Set swNewPart = swModel
         vBodyArr = swComp.<b>GetSectionedBodies</b>(swModelView)
         For Each vBody In vBodyArr
             Set swBody = vBody
             Set swFeat = swNewPart.<b>CreateFeatureFromBody3</b>(swBody, False, swCreateFeatureBodyCheck)
         Next vBody
    Else
        Debug.Print &quot;Model view does not contain a section view.&quot;
    End If         
</pre>
<pre>End Sub</pre>

</body>

</html>
