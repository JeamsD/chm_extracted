<html>

<head>
<title>Change Visibility of Sketch Block Instances (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,blocks">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Change Visibility of Sketch Block Instances (VBA)</h1>
<p>This example shows how to hide and show sketch block instances in a drawing 
document.</p>

<pre>'-------------------------------------------------
' Preconditions:
' 1. Drawing document containing a sketch
'    block with one or more sketch block instances is open.
' 2. The sketch block is selected in the FeatureManager design tree.
'
' Postconditions: All sketch block instances are hidden if visible, or
' shown if hidden.
'-------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModelDoc As SldWorks.ModelDoc2
Dim swSelMgr As SldWorks.SelectionMgr
Dim swFeature As SldWorks.Feature
Dim swBlockDefinition As SldWorks.SketchBlockDefinition
Dim blocks As Variant
Dim i As Long</pre>
<pre>Sub main()</pre>
<pre>Set swApp = Application.SldWorks
Set swModelDoc = swApp.<b>ActiveDoc</b>
Set swSelMgr = swModelDoc.<b>SelectionManager</b></pre>
<pre>' Select block is selected in FeatureManager design tree
Set swFeature = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
If swFeature Is Nothing Then
    MsgBox (&quot;Select a sketch block in the FeatureManager design tree, then rerun the macro.&quot;)
Else
    Set swBlockDefinition = swFeature.<b>GetSpecificFeature2</b>
    Debug.Print &quot;Feature type : &quot; &amp; swFeature.<b>GetTypeName2</b>
    If Not (swBlockDefinition Is Nothing) Then
        blocks = swBlockDefinition.<b>GetInstances</b>
        For i = LBound(blocks) To UBound(blocks)
        </pre>
<pre>            Dim swBlockInstance As SldWorks.SketchBlockInstance
            Set swBlockInstance = blocks(i)
            Debug.Print &quot;Sketch block instance: &quot; &amp; (i + 1)
            Debug.Print &quot;  Angle : &quot; &amp; swBlockInstance.<b>Angle</b>
            Debug.Print &quot;  Scale : &quot; &amp; swBlockInstance.<b>Scale2</b>
            </pre>
<pre>            ' Hide or show the sketch block instance
            Dim status As Long
            status = swBlockInstance.<b>Visible</b>
            Select Case status
                Case swAnnotationHidden
                    swBlockInstance.<b>Visible</b> = swAnnotationVisible
                    Debug.Print &quot;  Was hidden, now visible.&quot;
                Case swAnnotationVisible
                    swBlockInstance.<b>Visible</b> = swAnnotationHidden
                    Debug.Print &quot;  Was visible, now hidden.&quot;
                Case swAnnotationHalfHidden
                    MsgBox (&quot;This block is half hidden.&quot;)
                Case swAnnotationVisibilityUnknown
                    MsgBox (&quot;Failed to determine visibility of this block.&quot;)
            End Select
            </pre>
<pre>        Next i
    End If
    </pre>
<pre>    blocks = Empty
    </pre>
<pre>End If</pre>
<pre>End Sub</pre>

</body>

</html>
