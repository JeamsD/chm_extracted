<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Combine Assembly Components into Part Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2634>
<meta name=layout-width content=723>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">

 <META NAME="MS-HKWD" CONTENT="VBA examples,components">
</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Combine Assembly Components into Part Example (VBA)</h1>

<p>This example shows how to combine two assembly components into a part.</p>

<pre>'------------------------------------------------------------
' Preconditions:
' 1. Open <i>public_documents</i><b>\samples\tutorial\api\landing_gear.sldasm</b>.
' 2. In the FeatureManager design tree:
'    a. Click <b>oleostrut&lt;1&gt;</b>.
'    b. Ctrl+click <b>oleopiston&lt;1&gt;</b>.
'    NOTE: Both components are single-body components that
'    only contain solid bodies.
' 3. Verify that the specified part document template exists.
'
' Postconditions:
' 1. Creates a new part, which is a Boolean addition of both
'    selected components.
' 2. Click <b>Zoom to Fit</b>.
'
' NOTES:
' * This example:
'   * does not replace saving an assembly as a part.
'   * only illustrates the use of several geometric and
'     topological methods and properties.
' * Because the assembly is used elsewhere, do not save
'   changes.
'------------------------------------------------------------
Option Explicit</pre>
<pre>Sub main()</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swComp1 As SldWorks.Component2
    Dim swComp2 As SldWorks.Component2
    Dim swXform1 As SldWorks.MathTransform
    Dim swXform2 As SldWorks.MathTransform
    Dim swBody1 As SldWorks.Body2
    Dim swBody1Copy As SldWorks.Body2
    Dim swBody2 As SldWorks.Body2
    Dim swBody2Copy As SldWorks.Body2
    Dim vBodyResArr As Variant
    Dim vBodyRes As Variant
    Dim swBodyRes As SldWorks.Body2
    Dim swPartRes  As SldWorks.PartDoc
    Dim swFeatRes As SldWorks.Feature
    Dim nRetval As Long
    Dim bRet As Boolean</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swComp1 = swSelMgr.<b>GetSelectedObjectsComponent2</b>(1)
    Set swComp2 = swSelMgr.<b>GetSelectedObjectsComponent2</b>(2)
    Set swXform1 = swComp1.<b>Transform2</b>
    Set swXform2 = swComp2.<b>Transform2</b></pre>
<pre>    Set swBody1 = swComp1.<b>GetBody</b>
    Set swBody2 = swComp2.<b>GetBody</b>
    Set swBody1Copy = swBody1.<b>Copy</b>
    Set swBody2Copy = swBody2.<b>Copy</b></pre>
<pre>    bRet = swBody1Copy.<b>ApplyTransform</b>(swXform1)
    bRet = swBody2Copy.<b>ApplyTransform</b>(swXform2)</pre>
<pre>    vBodyResArr = swBody1Copy.<b>Operations2</b>(SWBODYADD, swBody2Copy, nRetval)
    Debug.Assert Not IsEmpty(vBodyResArr)</pre>
<pre>    Set swPartRes = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SOLIDWORKS 2014\templates\Part.prtdot&quot;, 0, 0, 0)
    For Each vBodyRes In vBodyResArr
        Set swBodyRes = vBodyRes
        Set swFeatRes = swPartRes.<b>CreateFeatureFromBody3</b>(swBodyRes, False, swCreateFeatureBodyCheck + swCreateFeatureBodySimplify): Debug.Assert Not swFeatRes Is Nothing
    Next</pre>
<pre>End Sub
</pre>

</body>

</html>
