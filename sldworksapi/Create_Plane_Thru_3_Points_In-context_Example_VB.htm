<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Create Plane thru 3 Points In-context Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2091>
<meta name=layout-width content=723>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">




 <META NAME="MS-HKWD" CONTENT="VBA examples,planes">

</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Create Plane thru 3 Points In-context Example (VBA)</h1>

<p>This example shows how to create a plane through 3 points in-context.</p>
<pre>'---------------------------------------------------
' Preconditions:
' 1. Open a fully resolved assembly.
' 2. Select a component.
' 3. Expand the selected component in the FeatureManager
'    design tree.
'
' Postconditions:
' 1. Creates a plane, passing through three points,
'    in the selected component.
' 2. Examine the selected component in the FeatureManager
'    design tree to verify that a 3D sketch and plane
'    were created.
'--------------------------------------------------
Option Explicit</pre>
<pre>'  Possible status values for AssemblyDoc::EditPart2
Public Enum swEditPartCommandStatus_e
    swEditPartFailure = -1
    swEditPartAsmMustBeSaved = -2
    swEditPartCompMustBeSelected = -3
    swEditPartCompMustBeResolved = -4
    swEditPartCompMustHaveWriteAccess = -5
    swEditPartSuccessful = 0
    swEditPartCompNotPositioned = &amp;H1
End Enum</pre>
<pre>Sub main()
    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swAssy As SldWorks.AssemblyDoc
    Dim swEditModel As SldWorks.ModelDoc2
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swSelData As SldWorks.SelectData
    Dim swPart As SldWorks.PartDoc
    Dim swSketchPt1 As SldWorks.SketchPoint
    Dim swSketchPt2 As SldWorks.SketchPoint
    Dim swSketchPt3 As SldWorks.SketchPoint
    Dim swPlane As SldWorks.RefPlane
    Dim nRetVal As Long
    Dim nInfo As Long
    Dim bRet As Boolean</pre>
<pre>    Set swApp = CreateObject(&quot;SldWorks.Application&quot;)
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swSelData = swSelMgr.<b>CreateSelectData</b>
    Set swAssy = swModel</pre>
<pre>    ' Start in-context edit
    nRetVal = swAssy.<b>EditPart2</b>(True, False, nInfo)
    Debug.Assert swEditPartSuccessful = nRetVal</pre>
<pre>    Set swEditModel = swModel</pre>
<pre>    ' Turn off snapping
    swEditModel.<b>SetAddToDB</b> True</pre>
<pre>    ' Insert part/component 3D sketch in-context
    swEditModel.<b>Insert3DSketch2</b> True</pre>
<pre>    ' Create points in part
    Set swSketchPt1 = swEditModel.<b>CreatePoint2</b>(0#, 0.02123307340457, 0.005485856156458)
    Set swSketchPt2 = swEditModel.<b>CreatePoint2</b>(0.04415646169588, 0.01166034702997, -0.00770979679615)
    Set swSketchPt3 = swEditModel.<b>CreatePoint2</b>(0#, -0.006247647329005, 0.007641244473859)</pre>
<pre>    ' Exit sketch but in assembly
    ' This gets you to editing part/component in-context
    swModel.<b>Insert3DSketch2</b> True</pre>
<pre>    ' Restore snapping
    swEditModel.<b>SetAddToDB</b> False</pre>
<pre>    swModel.<b>ClearSelection2</b> True

    bRet = swSketchPt1.<b>Select4</b>(True, swSelData): Debug.Assert bRet
    bRet = swSketchPt2.<b>Select4</b>(True, swSelData): Debug.Assert bRet
    bRet = swSketchPt3.<b>Select4</b>(True, swSelData): Debug.Assert bRet</pre>
<pre>    ' Create plane in part/component
    Set swPlane = swModel.<b>CreatePlaneThru3Points3</b>(True)
    Debug.Assert Not swPlane Is Nothing</pre>
<pre>    ' Go back to assembly
    ' End in-context edit
    swAssy.<b>EditAssembly</b></pre>
<pre>End Sub</pre>

</body>

</html>
