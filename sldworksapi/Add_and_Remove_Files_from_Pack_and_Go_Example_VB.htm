<html>

<head>
<title>Add and Remove Files from Pack and Go Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,Pack and Go">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Add and Remove Files from Pack and Go Example (VBA)</h1>
<p>This example shows how to add SOLIDWORKS, render reference, and 
non-SOLIDWORKS files to Pack and Go. This example also shows how to remove a 
non-SOLIDWORKS file from Pack and Go.</p>

<pre>'-------------------------------------------
' Preconditions:
' 1. Verify that the specified assembly exists.
' 2. Create<b> c:\PackAndGo</b>.
' 3. Open the Immediate window.
'
' Postconditions:
' 1. Gets the names of SOLIDWORKS, render reference, and
'    non-SOLIDWORKS files for Pack and Go.
' 2. Gets the the name of non-SOLIDWORKS file to remove.
' 3. Packs up SOLIDWORKS, render reference, and
'    non-SOLIDWORKS files and copies them to <b>c:\PackAndGo</b>.
' 4. Examine <b>c:\PackAndGo</b> and the Immediate window.
'-------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swPackAndGo As SldWorks.PackAndGo
Dim openFile As String
Dim errors As Long
Dim warnings As Long
Dim status As Boolean
Dim pgFileNames As Variant
Dim i As Long
Dim renderReferences As Variant
Dim otherFiles(1) As String
Dim delOtherFiles(0) As String
Dim myPath As String
Dim statuses As Variant</pre>
<pre>Sub main()</pre>
<pre>Set swApp = Application.SldWorks</pre>
<pre>' Open assembly document
openFile = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\EDraw\claw\claw-mechanism.sldasm&quot;
Set swModel = swApp.<b>OpenDoc6</b>(openFile, swDocASSEMBLY, swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
Set swModelDocExt = swModel.<b>Extension</b></pre>
<pre>' Get Pack and Go object
Debug.Print &quot;Pack and Go&quot;
Set swPackAndGo = swModelDocExt.<b>GetPackAndGo</b></pre>
<pre>' Get current paths and filenames of the assembly's documents
status = swPackAndGo.<b>GetDocumentNames</b>(pgFileNames)
Debug.Print &quot;&quot;
Debug.Print &quot;  Add SOLIDWORKS files' paths and filenames: &quot;
If (Not (IsEmpty(pgFileNames))) Then
    For i = 0 To UBound(pgFileNames)
        Debug.Print &quot;    The path and filename is: &quot; &amp; pgFileNames(i)
    Next i
End If</pre>
<pre>' Set document paths and names for Pack and Go
status = swPackAndGo.<b>SetDocumentSaveToNames</b>(pgFileNames)</pre>
<pre>' Get the render stock references in this assembly
' and print them to the Immediate window
Debug.Print &quot; &quot;
renderReferences = swModelDocExt.<b>GetRenderStockReferences</b>
Debug.Print &quot;  Add render references:&quot;
For i = 0 To UBound(renderReferences)
    Debug.Print &quot;    The path and filename is: &quot; &amp; renderReferences(i)
Next i</pre>
<pre>' Add render stock files to Pack and Go
status = swPackAndGo.<b>AddExternalDocuments</b>(renderReferences)</pre>
<pre>' Add other non-SOLIDWORKS files to Pack and Go
otherFiles(0) = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\edraw\claw\claw-mechanism.easm&quot;
otherFiles(1) = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\edraw\claw\claw-mechanism.emodel_debugonly.xml&quot;
Debug.Print &quot; &quot;
Debug.Print &quot;  Add non-SOLIDWORKS files:&quot;
For i = 0 To UBound(otherFiles)
    Debug.Print &quot;    The path and filename is: &quot; &amp; otherFiles(i)
Next i
status = swPackAndGo.<b>AddExternalDocuments</b>(otherFiles)</pre>
<pre>' Remove one of the non-SOLIDWORKS files from Pack and Go
Debug.Print &quot; &quot;
Debug.Print &quot;  Remove non-SOLIDWORKS file:&quot;
delOtherFiles(0) = otherFiles(0)
Debug.Print &quot;    The path and filename is: &quot; &amp; delOtherFiles(0)
status = swPackAndGo.<b>RemoveExternalDocuments</b>(delOtherFiles)
</pre>
<pre>' Override path where to save documents
myPath = &quot;c:\PackAndGo\&quot;
status = swPackAndGo.<b>SetSaveToName</b>(True, myPath)
</pre>
<pre>' Pack and Go both SOLIDWORKS and non-SOLIDWORKS files
statuses = swModelDocExt.<b>SavePackAndGo</b>(swPackAndGo)
</pre>
<pre>End Sub</pre>

<p>&nbsp;</p>

</body>

</html>
