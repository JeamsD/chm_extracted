<html>

<head>
<title>Get Projected Curve Feature Data Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,features">
<meta name=MS-HKWD content="VBA examples,curves">
<meta name=MS-HKWD content="VBA examples,projected curves">
<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Get Projected Curve Feature Data Example (VBA)</h1>
<p>This example shows how to get data for a projected curve feature.</p>

<pre>'--------------------------------------------------------------
' Preconditions:
' 1. Verify that the part document exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the specified part document.
' 2. Selects a face and sketches a spline on that face.
' 3. Selects the sketch of the spline and a face.
' 4. Inserts a projected curve feature.
' 5. Gets some projected curve feature data and prints it
'    to the Immediate window.
' 6. Examine the Immediate window, FeatureManager design tree, and
'    the graphics area.
'
' NOTE: Because the part is used elsewhere, do not save changes.
'----------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swSketchManager As SldWorks.SketchManager
Dim swSketchSegment As SldWorks.SketchSegment
Dim swFeature As SldWorks.Feature
Dim swSelectionManager As SldWorks.SelectionMgr
Dim swProjectionCurveFeatureData As SldWorks.ProjectionCurveFeatureData
Dim swSketch As SldWorks.Sketch
Dim pointArray As Variant
Dim points(10) As Double
Dim status As Boolean
Dim errors As Long
Dim warnings As Long
Dim fileName As String
</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks    </pre>
<pre>    'Open part document
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\block20.sldprt&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocPART, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)    </pre>
<pre>    'Sketch a spline on the selected face
    Set swModelDocExt = swModel.<b>Extension</b>
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, -4.99223104334874E-02, 3.96239999998897E-02, 7.38137362270663E-03, False, 0, Nothing, 0)
    Set swSketchManager = swModel.<b>SketchManager</b>
    swSketchManager.<b>InsertSketch</b> True
    swModel.<b>ClearSelection2</b> True
    points(0) = -6.24778997860176E-02
    points(1) = 7.29572078180673E-03
    points(2) = 0
    points(3) = -3.64588790258153E-02
    points(4) = 3.24586288177215E-02
    points(5) = 0
    points(6) = 1.04252377344665E-02
    points(7) = 1.40473535914225E-02
    points(8) = 0
    points(9) = 6.46002912861796E-02
    points(10) = 1.00590221094308E-02
    pointArray = points
    Set swSketchSegment = swSketchManager.<b>CreateSpline2</b>((pointArray), False)
    swSketchManager.<b>InsertSketch</b> True    </pre>
<pre>    swModel.<b>ClearSelection2</b> True    </pre>
<pre>    'Insert projected curve
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch2&quot;, &quot;SKETCH&quot;, 0, 0, 0, True, 0, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, -4.97146993259321E-02, 0, -2.56283866693252E-02, True, 0, Nothing, 0)
    Set swFeature = swModel.<b>InsertProjectedSketch2</b>(1)</pre>
<pre>    'Get projected curve data
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Curve1&quot;, &quot;REFCURVE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swSelectionManager = swModel.<b>SelectionManager</b>
    Set swFeature = swSelectionManager.<b>GetSelectedObject6</b>(1, -1)
    Set swProjectionCurveFeatureData = swFeature.<b>GetDefinition</b>
    swProjectionCurveFeatureData.<b>AccessSelections</b> swModel, Nothing
    Debug.Print &quot;Is reversed = &quot; &amp; swProjectionCurveFeatureData.<b>Reverse</b>
    Debug.Print &quot;Number of targeted faces = &quot; &amp; swProjectionCurveFeatureData.<b>GetFaceArrayCount</b>
    Set swFeature = swProjectionCurveFeatureData.<b>Sketch</b>
    Set swSketch = swFeature.<b>GetSpecificFeature2</b>
    Debug.Print &quot;Name of sketch = &quot; &amp; swFeature.<b>Name</b>
    swProjectionCurveFeatureData.<b>ReleaseSelectionAccess</b></pre>
<pre>End Sub</pre>

</body>

</html>
