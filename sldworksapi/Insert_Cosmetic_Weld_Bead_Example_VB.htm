<html>

<head>
<title>Insert Cosmetic Weld Bead Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,weld beads">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Insert Cosmetic Weld Bead Example (VBA)</h1>
<p>This example shows how to insert a cosmetic weld bead feature and access an 
existing weld bead feature's 
properties.</p>

<pre>'----------------------------------------------------------------------------
' Preconditions:
' 1. Open <i>public_documents</i><b>\samples\tutorial\api\Insert_weld.sldprt</b>.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Inserts a fillet weld, <b>Weld Bead2</b>, and adds it to the Weld Folder in
'    the FeatureManager design tree.
' 2. Inspect the Immediate window for <b>Weld Bead1</b> settings and properties.
' 3. Expand the Weld Folder and 20.08in Fillet Weld(1) folders in the 
'    FeatureManager design tree to verify step 1.
'
' NOTE: Because the model is used elsewhere, do not save changes.
'---------------------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swFeatureManager As SldWorks.FeatureManager
Dim swCosmeticWeldBeadFeatureData As SldWorks.CosmeticWeldBeadFeatureData
Dim swWeldFolder As SldWorks.CosmeticWeldBeadFolder
Dim swSelectionMgr As SldWorks.SelectionMgr
Dim swFeature As SldWorks.Feature
Dim edges As Variant
Dim status As Boolean</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swModelDocExt = swModel.<b>Extension</b>    </pre>
<pre>    'Insert Weld Bead2
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, -3.44320907599354E-02, 1.70180000000641E-02, -2.27566098720899E-03, False, 0, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, -1.61637176506133E-02, 5.03122973344716E-02, -1.3752238241409E-03, True, 0, Nothing, 0)
    Set swFeatureManager = swModel.<b>FeatureManager</b>
    Set swFeature = swFeatureManager.<b>InsertCosmeticWeldBead</b>(0.51)    </pre>
<pre>    swModel.<b>ClearSelection2</b> True</pre>
<pre>    'Get Weld Bead1 and get and set some properties
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Weld Bead1&quot;, &quot;COSMETIC_WELD&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swSelectionMgr = swModel.<b>SelectionManager</b>
    Set swFeature = swSelectionMgr.<b>GetSelectedObject6</b>(1, 0)
    Set swCosmeticWeldBeadFeatureData = swFeature.<b>GetDefinition</b>
    swCosmeticWeldBeadFeatureData.<b>AccessSelections</b> swModel, Nothing</pre>
<pre>    Debug.Print &quot;Weld Bead1 settings: &quot;
    Debug.Print &quot;   Weld bead size: &quot; &amp; swCosmeticWeldBeadFeatureData.<b>BeadSize</b> * 39.37 &amp; &quot; in&quot;
    Debug.Print &quot;   Tangent propagation? &quot; &amp; swCosmeticWeldBeadFeatureData.<b>TangentPropagation</b>
    Debug.Print &quot;   Weld sides as defined in swCosmeticWeldBeadSide_e (1 = swCosmeticWeldBeadSide_selection): &quot; &amp; swCosmeticWeldBeadFeatureData.<b>Side</b>
    Debug.Print &quot;   From/to length properties enabled? &quot; &amp; swCosmeticWeldBeadFeatureData.<b>FromToLength</b>
    If swCosmeticWeldBeadFeatureData.<b>FromToLength</b> = False Then
        swCosmeticWeldBeadFeatureData.<b>FromToLength</b> = True
    End If
    If swCosmeticWeldBeadFeatureData.<b>FromToLength</b> = True Then
        Debug.Print &quot;   Start weld at: &quot; &amp; swCosmeticWeldBeadFeatureData.<b>FromToStartPoint</b>
        Debug.Print &quot;   Weld length: &quot; &amp; swCosmeticWeldBeadFeatureData.<b>FromToWeldLength</b>
        Debug.Print &quot;   Reverse weld? &quot; &amp; swCosmeticWeldBeadFeatureData.<b>FromToReverse</b>
    End If
    Debug.Print &quot;   Intermittent weld properties enabled? &quot; &amp; swCosmeticWeldBeadFeatureData.<b>IntermittentWeld</b>
    If swCosmeticWeldBeadFeatureData.<b>IntermittentWeld</b> = False Then
        swCosmeticWeldBeadFeatureData.<b>IntermittentWeld</b> = True
    End If
    If swCosmeticWeldBeadFeatureData.<b>IntermittentWeld</b> = True Then
        If swCosmeticWeldBeadFeatureData.<b>GapOrPitch</b> = True Then
            Debug.Print &quot;   Intermittent gap: &quot; &amp; swCosmeticWeldBeadFeatureData.<b>Gap</b>
            Debug.Print &quot;   Intermittent weld length: &quot; &amp; swCosmeticWeldBeadFeatureData.<b>IntermittentWeldLength</b>
        Else
            Debug.Print &quot;   Intermittent pitch: &quot; &amp; swCosmeticWeldBeadFeatureData.<b>Pitch</b>
            Debug.Print &quot;   Intermittent weld length: &quot; &amp; swCosmeticWeldBeadFeatureData.<b>IntermittentWeldLength</b>
            Debug.Print &quot;   Stagger welds if welding on both sides? &quot; &amp; swCosmeticWeldBeadFeatureData.<b>Staggered</b>
        End If
    End If
    edges = swCosmeticWeldBeadFeatureData.<b>GetReferenceEdges</b>
    Debug.Print &quot;   Number of reference edges: &quot; &amp; UBound(edges)</pre>
<pre>    Set swWeldFolder = swCosmeticWeldBeadFeatureData.<b>GetWeldBeadFolder</b>    
</pre>
<pre>    Debug.Print &quot;Weld Bead1 properties:&quot;
    Debug.Print &quot;   Weld material was: &quot; &amp; swWeldFolder.<b>Material</b>
    swWeldFolder.<b>Material</b> = &quot;Steel&quot;
    Debug.Print &quot;   Weld material is now: &quot; &amp; swWeldFolder.<b>Material</b>
    Debug.Print &quot;   Welding cost per unit mass: &quot; &amp; swWeldFolder.<b>CostPerUnitMass</b>
    Debug.Print &quot;   Weld mass per unit length: &quot; &amp; swWeldFolder.<b>MassPerUnitLength</b>
    Debug.Print &quot;   Number of weld passes: &quot; &amp; swWeldFolder.<b>NumberOfWeldPasses</b>
    Debug.Print &quot;   Weld process: &quot; &amp; swWeldFolder.<b>Process</b>
    Debug.Print &quot;   Welding time per unit length: &quot; &amp; swWeldFolder.<b>TimePerUnitLength</b>
    Debug.Print &quot;   Total welding cost: $&quot; &amp; swWeldFolder.<b>TotalCost</b>
    Debug.Print &quot;   Total weld length: &quot; &amp; swWeldFolder.<b>TotalLength</b>
    Debug.Print &quot;   Total weld mass: &quot; &amp; swWeldFolder.<b>TotalMass</b> &amp; &quot; lb&quot;
    Debug.Print &quot;   Total number of welds: &quot; &amp; swWeldFolder.<b>TotalNumber</b>
    Debug.Print &quot;   Total welding time: &quot; &amp; swWeldFolder.<b>TotalTime</b> &amp; &quot; sec&quot;   
</pre>
<pre>    status = swFeature.<b>ModifyDefinition</b>(swCosmeticWeldBeadFeatureData, swModel, Nothing)   </pre>
<pre>End Sub</pre>

</body>

</html>
