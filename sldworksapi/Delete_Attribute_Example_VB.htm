<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Delete Attribute Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,attributes">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=850>
<meta name=layout-width content=750>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>

<h1>Delete Attribute Example (VBA)</h1>

<p>This example shows how to delete the selected attribute.</p>

<pre>'-------------------------------------------------------------
' Preconditions:
' 1. Verify that the specified part document to open exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the specified part document.
' 2. Selects <b>Cut-Extrude3</b>.
' 3. Adds attribute <b>att 1</b> to the selected feature.
' 4. Rebuilds the part.
' 5. Deletes attribute <b>att 1</b>.
' 6. Expand <b>Cut-Extrude3</b> in the FeatureManager design tree
'    and examine the Immediate window to verify step 5.
'
' NOTE: Because the part is used elsewhere, do not save changes.
'----------------------------------------------------------
Option Explicit</pre>
<pre>Sub main()</pre>
<pre>    '1 = invisible
    '0 = visible
    Const CreateVisible As Long = 0
    Const AttDefName As String = &quot;XML_string&quot;
    Const AttLenDimName As String = &quot;att_name&quot;
    Const AttLenValueName As String = &quot;att_value&quot;
    Const AttRootName As String = &quot;att&quot;
    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swModelDocExt As SldWorks.ModelDocExtension
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swFeat As SldWorks.Feature
    Dim swEnt As SldWorks.Entity
    Dim swAttDef As SldWorks.AttributeDef
    Dim swAtt As SldWorks.Attribute
    Dim swParamName As SldWorks.Parameter
    Dim swParamValue As SldWorks.Parameter
    Dim AttName As String
    Dim i As Long
    Dim bRet As Boolean
    Dim fileName As String
    Dim errors As Long
    Dim warnings As Long    </pre>
<pre>    Set swApp = CreateObject(&quot;SldWorks.Application&quot;)    </pre>
<pre>    'Open part document
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\smartcomponents\bearing.sldprt&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocPART, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)    </pre>
<pre>    'Select feature
    Set swModelDocExt = swModel.<b>Extension</b>
    bRet = swModelDocExt.<b>SelectByID2</b>(&quot;Cut-Extrude3&quot;, &quot;BODYFEATURE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swFeat = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swEnt = swFeat
    </pre>
<pre>    'Set attribute att 1
    Set swAttDef = swApp.<b>DefineAttribute</b>(AttDefName)
    bRet = swAttDef.<b>AddParameter</b>(AttLenDimName, swParamTypeString, 0#, 0)
    bRet = swAttDef.<b>AddParameter</b>(AttLenValueName, swParamTypeDouble, 0#, 0)
    bRet = swAttDef.<b>Register</b>
    While swAtt Is Nothing
        ' Get a unique attribute name
        i = i + 1
        AttName = AttRootName + Str(i)
        Set swAtt = swAttDef.<b>CreateInstance5</b>(swModel, swEnt, AttName, CreateVisible, swThisConfiguration)
    Wend
    Set swParamName = swAtt.<b>GetParameter</b>(AttLenDimName)
    Set swParamValue = swAtt.<b>GetParameter</b>(AttLenValueName)
    bRet = swParamName.<b>SetStringValue2</b>(AttName &amp; &quot; - &quot; &amp; AttLenDimName, swAllConfiguration, &quot;&quot;)
    bRet = swParamValue.<b>SetDoubleValue2</b>(i * 10, swAllConfiguration, &quot;&quot;)
    If Not swAtt Is Nothing Then
        Debug.Print &quot;Attribute &quot; &amp; AttName &amp; &quot; created.&quot;
    Else
        Debug.Print &quot;Attribute &quot; &amp; AttName &amp; &quot; not created.&quot;
    End If
    </pre>
<pre>    swModel.<b>ForceRebuild3</b> True
    </pre>
<pre>    'Delete attribute att 1
    bRet = swModelDocExt.<b>SelectByID2</b>(&quot;att 1&quot;, &quot;ATTRIBUTE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swFeat = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swAtt = swFeat.<b>GetSpecificFeature2</b>
    AttName = swAtt.<b>GetName</b>
    bRet = swAtt.<b>Delete</b>(True)
    If bRet Then
        Debug.Print &quot;Attribute &quot; &amp; AttName &amp; &quot; deleted.&quot;
    Else
        Debug.Print &quot;Attritute &quot; &amp; AttName &amp; &quot; not deleted.&quot;
    End If
    </pre>
<pre>End Sub</pre>

</body>

</html>
