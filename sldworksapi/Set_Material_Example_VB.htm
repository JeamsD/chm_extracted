<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Set Material Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,bodies">
<meta name=MS-HKWD content="VBA examples,materials">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=1774>
<meta name=layout-width content=926>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="ApiHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>

<h1>Set Material Example (VBA)</h1>

<p>This example shows how to get the names of the material schema, material 
 databases, and bodies in a part document. This example also shows how 
 to apply a SOLIDWORKS Material to all of the bodies in a part document.</p>

<pre>'----------------------------------------------------
' Preconditions:
' 1. Verify that the specified document exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the specified part document.
' 2. Applies the material <b>ABS PC (polycarbonate plastic)</b>
'    from the SOLIDWORKS Material database to all
'    bodies in the part.
' 3. To verify, examine:
'    * DisplayManager tab
'    * graphics area
'    * Immediate window
'
' NOTE: Because the part document is used elsewhere,
' do not save changes.
'----------------------------------------------------
Option Explicit</pre>
<pre>Sub main()</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swPart As SldWorks.PartDoc
    Dim swBody As SldWorks.Body2
    Dim errors As Long
    Dim warnings As Long
    Dim vMatDBarr As Variant
    Dim vMatDB As Variant
    Dim Bodies As Variant
    Dim BodyMaterialError As Long
    Dim sMatName As String
    Dim sMatDB As String
    Dim itr As Long
    Dim boolstat As Boolean</pre>
<pre>    Set swApp = Application.SldWorks</pre>
<pre>    ' Open the document
    Set swModel = swApp.<b>OpenDoc6</b>(&quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\multibody\multi_inter.sldprt&quot;, swDocPART, swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swPart = swModel</pre>
<pre>    ' Get the material schema and names
    ' of available materials databases
    vMatDBarr = swApp.<b>GetMaterialDatabases</b>
    Debug.Print &quot;Material schema pathname = &quot; &amp; swApp.<b>GetMaterialSchemaPathName</b>
    For Each vMatDB In vMatDBarr
        Debug.Print &quot;  Material database: &quot; &amp; vMatDB
    Next
    </pre>
<pre>    Debug.Print &quot;&quot;</pre>
<pre>    Bodies = swPart.<b>GetBodies2</b>(swAllBodies, False)
    For itr = 0 To UBound(Bodies)
        Set swBody = Bodies(itr)
        ' Get the name of the body
        Debug.Print swBody.<b>Name
</b>        boolstat = swBody.<b>Select2</b>(False, Nothing)
        ' Set the SOLIDWORKS material for that body
        BodyMaterialError = swBody.<b>SetMaterialProperty</b>(&quot;Default&quot;, &quot;solidworks materials.sldmat&quot;, &quot;ABS PC&quot;)</pre>
<pre>        ' Comment out previous statement and uncomment following statement to use custom material
        'BodyMaterialError = swBody.<b>SetMaterialProperty</b>(&quot;Default&quot;, &quot;custom materials.sldmat&quot;, &quot;Custom Plastic&quot;)</pre>
<pre>        ' Get the names of the body's material and the
        ' database to which it belongs
        sMatName = swBody.<b>GetMaterialPropertyName</b>(&quot;&quot;, sMatDB)
        If sMatName = &quot;&quot; Then
            Debug.Print (&quot;Body &quot; &amp; itr &amp; &quot;'s material name: No material applied&quot;)
        Else
            Debug.Print (&quot;Body &quot; &amp; itr &amp; &quot;'s material name: &quot; &amp; sMatName)
            Debug.Print (&quot;Body &quot; &amp; itr &amp; &quot;'s material database: &quot; &amp; sMatDB)
            Debug.Print (&quot; &quot;)
        End If</pre>
<pre>    Next itr    
</pre>
<pre>End Sub</pre>

</body>

</html>
