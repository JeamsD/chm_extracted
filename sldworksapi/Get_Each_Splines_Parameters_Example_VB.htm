<html>

<head>
<title>Get Each Spline's Parameters Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,splines">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Get Each Spline's Parameters Example (VBA)</h1>
<p>This example shows how to get each spline's parameters in a sketch containing 
multiple splines.</p>

<pre>'----------------------------------------------------
' Preconditions:
' 1. Verify that the specified part template exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Creates a sketch containing two splines.
' 2. Gets each spline's dimension, order, periodicity,
'    control point, and knot point data.
' 3. Examine the Immediate window.
'-----------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swSelMgr As SldWorks.SelectionMgr
Dim swSketchSegment As SldWorks.SketchSegment
Dim swFeature As SldWorks.Feature
Dim swSketch As SldWorks.Sketch
Dim swSplineParamData As SldWorks.SplineParamData
Dim swSketchMgr As SldWorks.SketchManager
Dim points(11) As Double
Dim pointArray As Variant
Dim varCtrlPoints As Variant
Dim varKnotPoints As Variant
Dim status As Boolean
Dim i As Integer
Dim j As Integer
Dim k As Integer
Dim splineCount As Long
Dim splinePointCount As Long</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SOLIDWORKS 2017\templates\Part.prtdot&quot;, 0, 0, 0)</pre>
<pre>    'Create a sketch with two splines
    'First spline
    points(0) = -0.185955019567672
    points(1) = 4.16208582005027E-02
    points(2) = 0
    points(3) = -8.62492383138544E-02
    points(4) = 4.03922105323034E-02
    points(5) = 0
    points(6) = -6.72740896322921E-02
    points(7) = 5.40598971292923E-02
    points(8) = 0
    points(9) = -1.41436733240425E-02
    points(10) = -5.70437188125084E-03
    points(11) = 0
    pointArray = points
    Set swSketchMgr = swModel.<b>SketchManager</b>
    Set swSketchSegment = swSketchMgr.<b>CreateSpline</b>((pointArray))
    swModel.<b>ClearSelection2</b> True
    'Second spline
    points(0) = -8.38342193907238E-02
    points(1) = -3.80341664350112E-02
    points(2) = 0
    points(3) = -6.55490761158148E-02
    points(4) = -1.79490921124739E-02
    points(5) = 0
    points(6) = -1.79387030603664E-02
    points(7) = -6.81344637902441E-02
    points(8) = 0
    points(9) = 6.34819349185705E-02
    points(10) = -3.29692207162395E-02
    points(11) = 0
    pointArray = points
    Set swSketchSegment = swSketchMgr.<b>CreateSpline</b>((pointArray))
    swModel.<b>ClearSelection2</b> True
    'Sketch
    swSketchMgr.<b>InsertSketch</b> (True)    </pre>
<pre>    'Get each spline's dimension, order, periodicity, control point, and knot data
    status = swModel.<b>Extension</b>.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCH&quot;, 0, 0, 0, False, 0, Nothing, swSelectOptionDefault)
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swFeature = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swSketch = swFeature.<b>GetSpecificFeature2</b>
    Debug.Print swFeature.<b>Name</b>
    Debug.Print &quot;&quot;    </pre>
<pre>    splineCount = swSketch.<b>GetSplineCount</b>(splinePointCount)    </pre>
<pre>    For i = 1 To splineCount
        Debug.Print &quot;Spline &quot; &amp; i &amp; &quot;: &quot;
        Set swSplineParamData = swSketch.<b>GetSplineParams5</b>(True, i - 1)
        Debug.Print &quot;  Dimension: &quot; &amp; swSplineParamData.<b>Dimension</b>
        Debug.Print &quot;  Order: &quot; &amp; swSplineParamData.<b>Order</b>
        Debug.Print &quot;  Periodicity: &quot; &amp; swSplineParamData.<b>Periodic</b>
        Debug.Print &quot;  Number of control points: &quot; &amp; swSplineParamData.<b>ControlPointsCount</b>
        status = swSplineParamData.<b>GetControlPoints</b>(varCtrlPoints)
        Debug.Print &quot;  Control points:&quot;        </pre>
<pre>        For j = 0 To UBound(varCtrlPoints)
            Debug.Print &quot;      &quot; &amp; varCtrlPoints(j)
        Next j        </pre>
<pre>        Debug.Print &quot;  Number of knots: &quot; &amp; swSplineParamData.<b>KnotPointsCount</b>
        status = swSplineParamData.<b>GetKnotPoints</b>(varKnotPoints)
        Debug.Print &quot;    Knot points:&quot;        </pre>
<pre>        For k = 0 To UBound(varKnotPoints)
            Debug.Print &quot;      &quot; &amp; varKnotPoints(k)
        Next k    </pre>
<pre>    Next i    </pre>
<pre>End Sub</pre>

</body>

</html>
