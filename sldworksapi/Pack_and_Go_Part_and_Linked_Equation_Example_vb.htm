<html>

<head>
<title>Pack and Go Part and Linked Equation Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,Pack and Go">
<meta name=MS-HKWD content="VBA examples,equations">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Pack and Go Part and Linked Equation Example(VBA)</h1>
<p>This example shows how to determine if a part document includes any equations 
and whether those equations are linked files. The example also shows how to add the 
part document and any linked equations to Pack and Go.</p>

<pre>'----------------------------------------
' Preconditions:
' 1. Verify that the specified part and equation documents 
'    exist.
' 2. Verify that<b> c:\temp</b> exists.
' 3. Open the Immediate window.
'
' Postconditions:
' 1. Opens the specified part document.
' 2. Adds the part and linked equation documents
'    to Pack and Go and copies the files to
'    <b>c:\temp</b>.
' 3. To verify, examine <b>c:\temp</b>.
'
' NOTE: Because the model is used elsewhere,
' do not save changes.
'-----------------------------------------</pre>
<pre>Option Explicit</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swModelDocExt As SldWorks.ModelDocExtension
    Dim swEqnMgr As SldWorks.EquationMgr
    Dim swPackAndGo As SldWorks.PackAndGo
    Dim fileName As String
    Dim errors As Long
    Dim warnings As Long
    Dim i As Long
    Dim nCount As Long
    Dim eqnLinked As Boolean
    Dim pgFileNames As Variant
    Dim status As Boolean
    Dim statuses As Variant
    Dim myPath As String    </pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks    </pre>
<pre>    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\microphonehousing.sldprt&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocPART, swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swModelDocExt = swModel.<b>Extension</b>
    Debug.Print &quot;File = &quot; &amp; swModel.<b>GetPathName</b>
    Debug.Print &quot; &quot;    </pre>
<pre>    ' Get Equation manager object
    Set swEqnMgr = swModel.<b>GetEquationMgr</b>    </pre>
<pre>    ' List the equations, get whether they're linked
    ' to files, make sure any equations are up to date,
    ' and get the paths where they're stored
    nCount = swEqnMgr.<b>GetCount</b>
    For i = 0 To nCount - 1
        Debug.Print &quot;  Equation #&quot; &amp; i &amp; &quot; = &quot; &amp; swEqnMgr.<b>Equation</b>(i)        </pre>
<pre>        eqnLinked = swEqnMgr.<b>LinkToFile</b>
        Debug.Print &quot;  Equation linked to file? &quot; &amp; eqnLinked        </pre>
<pre>        If eqnLinked Then</pre>
<pre>            'Make sure equations are up to date
             swEqnMgr.<b>UpdateValuesFromExternalEquationFile</b>

            'Get path and name of linked equation file
            Debug.Print &quot;  Path and file name of linked equation: &quot; &amp; swEqnMgr.<b>FilePath</b>
        End If
    Next i        </pre>
<pre>    Debug.Print &quot; &quot;        </pre>
<pre>    ' Get Pack and Go object
    Debug.Print &quot;  Pack and Go&quot;
    Set swPackAndGo = swModelDocExt.<b>GetPackAndGo</b>     </pre>
<pre>    ' Get current paths and names of the documents
    status = swPackAndGo.<b>GetDocumentNames</b>(pgFileNames)
    Debug.Print &quot;    Add these SOLIDWORKS files to Pack and Go: &quot;
    If (Not (IsEmpty(pgFileNames))) Then
    For i = 0 To UBound(pgFileNames)
        Debug.Print &quot;      <font size="-1" face="Courier New">The file to pack up is:</font> &quot; &amp; pgFileNames(i)
    Next i
    End If    </pre>
<pre>    ' Set document paths and names for Pack and Go
    status = swPackAndGo.<b>SetDocumentSaveToNames</b>(pgFileNames)</pre>
<pre>    ' Override path where to save documents
    myPath = &quot;c:\temp\&quot;
    status = swPackAndGo.<b>SetSaveToName</b>(True, myPath)</pre>
<pre>    ' Pack and Go both SOLIDWORKS and non-SOLIDWORKS files
    statuses = swModelDocExt.<b>SavePackAndGo</b>(swPackAndGo)</pre>
<pre>End Sub</pre>

<p>&nbsp;</p>

</body>

</html>
