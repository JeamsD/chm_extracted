<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Get Section Properties for Faces from Section Planes Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="Properties,section">
<meta name=MS-HKWD content="Section properties">
<meta name=MS-HKWD content="VBA examples,section properties">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=3878>
<meta name=layout-width content=789>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>

<h1>Get Section Properties for Faces from Section Planes Example (VBA)</h1>

<p>This example shows how to get the section properties of the selected 
 items.</p>

<p><b>NOTE</b>: Through the user interface, it is possible 
 to extract the&nbsp;<!--kadov_tag{{</spaces>}}-->section 
 properties from various combinations of&nbsp;<!--kadov_tag{{</spaces>}}-->selected 
 entities. This is accessible through the SOLIDWORKS <!--kadov_tag{{</spaces>}}-->
API with&nbsp;<!--kadov_tag{{</spaces>}}-->IModelDocExtension::GetSectionProperties.&nbsp;<!--kadov_tag{{</spaces>}}-->This 
 method has only one input parameter, an array of entities&nbsp;<!--kadov_tag{{</spaces>}}-->to 
 consider with the current selection set. <!--kadov_tag{{</spaces>}}-->The 
 entity array must only contain these types of&nbsp;<!--kadov_tag{{</spaces>}}-->objects: Face2 and Sketch. <!--kadov_tag{{</spaces>}}-->For 
 planar faces and reference surfaces, this is the&nbsp;<!--kadov_tag{{</spaces>}}-->Face2 
 object. A sketch corresponds to a <!--kadov_tag{{</spaces>}}-->Sketch 
 object. However, for a face on a section plane,&nbsp;<!--kadov_tag{{</spaces>}}-->there 
 is no API object.&nbsp;<!--kadov_tag{{</spaces>}}-->This 
 sample code shows how to use this method <!--kadov_tag{{</spaces>}}-->to 
 allow for faces obtained from section planes.</p>
<pre>'-----------------------------------------------------
' Preconditions:
' 1. Open a part, fully resolved assembly, or drawing.
' 2. Select one of the following:
'    * Sketch
'    * Planar model face
'    * Face on a section plane
'    * Crosshatch section face in a section view
'      in a drawing or a sketch
' 3. Open the Immediate window.
'
' Postconditions:
' 1. Maintains the selection set. (See NOTES)
' 2. Examine the Immediate window.
'
' NOTES:
' * Array of objects passed into this method are
'   added to the selection set.
' * Outputted values are the same as those obtained
'   through the user interface.
' * Reasons for failures are the same as those
'   in the user interface.
'---------------------------------------------------
Option Explicit</pre>
<pre>Sub main()
    Const PI As Double = 3.14159</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swModelExt As SldWorks.ModelDocExtension
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swFace As SldWorks.Face2
    Dim swFeat() As SldWorks.Feature
    Dim swFaceEnt() As SldWorks.Entity
    Dim swSketch As SldWorks.Sketch
    Dim nSelType As Long
    Dim swSelObj() As Object
    Dim vSelObj As Variant
    Dim vSectionProp As Variant
    Dim nSelCount As Long
    Dim nNumObj As Long
    Dim i As Long</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swModelExt = swModel.<b>Extension</b>
    Set swSelMgr = swModel.<b>SelectionManager</b></pre>
<pre>    Debug.Print &quot;File = &quot; &amp; swModel.<b>GetPathName</b></pre>
<pre>    nSelCount = swSelMgr.<b>GetSelectedObjectCount</b>
    For i = 1 To nSelCount
        nSelType = swSelMgr.<b>GetSelectedObjectType2</b>(i)
        Debug.Print &quot;  SelType(&quot; &amp; i &amp; &quot;) = &quot; &amp; nSelType
        Select Case nSelType
            Case swSelFACES, swSelREFSURFACES
                nNumObj = nNumObj + 1
                ReDim Preserve swSelObj(nNumObj - 1)
                ReDim Preserve swFaceEnt(nNumObj - 1)
                Set swFace = swSelMgr.<b>GetSelectedObject5</b>(i)
                Set swFaceEnt(nNumObj - 1) = swFace
                Set swSelObj(nNumObj - 1) = swFace
            Case swSelSKETCHES
                nNumObj = nNumObj + 1
                ReDim Preserve swSelObj(nNumObj - 1)
                ReDim Preserve swFeat(nNumObj - 1)
                Set swFeat(nNumObj - 1) = swSelMgr.<b>GetSelectedObject5</b>(i)
                Set swSketch = swFeat(nNumObj - 1).<b>GetSpecificFeature</b>
                Set swSelObj(nNumObj - 1) = swSketch
                Debug.Print &quot;    &quot; &amp; swFeat(nNumObj - 1).Name</pre>
<pre>            Case swSelMANIPULATORS
                ' Section face in a part or assembly when in a section view
                ' There is no corresponding API object for this,
                ' so leave it selected
        End Select
    Next i
    Debug.Print &quot;&quot;</pre>
<pre>    ' Deselect faces and sketches; otherwise, user-interface selections
    ' are added to array parameter; leave section faces selected
    If Not IsEmpty(swFaceEnt) Then
        For i = 0 To UBound(swFaceEnt)
            swFaceEnt(i).<b>DeSelect</b>
        Next i
    End If</pre>
<pre>    If Not IsEmpty(swFeat) Then
        For i = 0 To UBound(swFeat)
            swFeat(i).<b>DeSelect</b>
        Next i
    End If
    
</pre>
<pre>    vSelObj = swSelObj</pre>
<pre>    ' This adds the array of faces or sketches to the selection set
    ' Because the faces or sketches have been deselected, this
    ' preserves the selection set
    vSectionProp = swModelExt.<b>GetSectionProperties</b>((vSelObj))</pre>
<pre>    ' Return code from:
    '   IModelDocExtension::GetSectionProperties
    '       0 = success
    '       1 = invalid input
    '       2 = selected faces are not in the same or parallel planes
    '       3 = unable to compute section properties
    Debug.Print &quot;  Return code              = &quot; &amp; vSectionProp(0)
    Debug.Print &quot;&quot;
    Debug.Print &quot;  Area                     = &quot; &amp; vSectionProp(1) * 1000000# &amp; &quot; mm^2&quot;
    Debug.Print &quot;  Centroid                 = (&quot; &amp; vSectionProp(2) * 1000# &amp; &quot;, &quot; &amp; vSectionProp(3) * 1000# &amp; &quot;, &quot; &amp; vSectionProp(4) * 1000# &amp; &quot;) mm&quot;
    Debug.Print &quot;  Ixx                      = &quot; &amp; vSectionProp(5) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Iyy                      = &quot; &amp; vSectionProp(6) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Izz                      = &quot; &amp; vSectionProp(7) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Ixy                      = &quot; &amp; vSectionProp(8) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Izx                      = &quot; &amp; vSectionProp(9) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Iyz                      = &quot; &amp; vSectionProp(10) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Polar MOI                = &quot; &amp; vSectionProp(11) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Angle princ &amp; part axes  =  &quot; &amp; vSectionProp(12) * 180# / PI &amp; &quot; deg&quot;
    Debug.Print &quot;  Ix                       = &quot; &amp; vSectionProp(13) * 1000000000000# &amp; &quot; mm^4&quot;
    Debug.Print &quot;  Iy                       = &quot; &amp; vSectionProp(14) * 1000000000000# &amp; &quot; mm^4&quot;</pre>
<pre>End Sub
</pre>

</body>

</html>
