<html>

<head>
<title>Export BOM's Second Column to BOM Table Area Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,3D PDF">
<meta name=MS-HKWD content="VBA examples,MBD">
<meta name=MS-HKWD content="VBA examples,SOLIDWORKS MBD">
<meta name=MS-HKWD content="VBA examples,bill of materials">
<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Export BOM's Second Column to BOM Table Area Example (VBA)</h1>
<p>This example shows how to export a BOM's second column to a BOM Table Area of 
a SOLIDWORKS MBD 3D PDF.</p>

<pre>'----------------------------------------------------------------------------
' Preconditions:
' 1. Verify that:
'    * specified assembly,
'    * specified SOLIDWORKS MBD 3D PDF theme, and
'    * <b>c:\temp</b> exist.
' 2. Open an Immediate window.
'
' Postconditions:
' 1. Inserts an indented BOM table in the assembly.
' 2. Gets the title of the second column in the BOM table
'    to export that column to the SOLIDWORKS MBD 3D PDF.
' 3. Gets the name of the BOM to map to the SOLIDWORKS
'    MBD 3D PDF.
' 4. Gets the SOLIDWORKS MBD 3D PDF data object.
'    a. Sets to display the SOLIDWORKS MBD 3D PDF after
'       publishing it.
'    b. Sets the path for the SOLIDWORKS MBD 3D PDF.
'    c. Sets the SOLIDWORKS MBD 3D PDF theme.
'    d. Sets the standard views for the SOLIDWORKS MBD 3D PDF.
'    e. Maps the BOM and exports its second column to a BOM
'       Table Area in the SOLIDWORKS MBD 3D PDF.
'    f. Publishes and displays the SOLIDWORKS MBD 3D PDF.
' 5. Examine <b>c:\temp\MBDAssembly1.PDF</b> and the Immediate window.
'
' NOTE: Because the assembly is used elsewhere, do not save changes.
'---------------------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swBOMAnnotation As SldWorks.BomTableAnnotation
Dim swTableAnnotation As SldWorks.TableAnnotation
Dim swBOMFeature As SldWorks.BomFeature
Dim swMBDPdfData As SldWorks.MBD3DPdfData
Dim fileName As String
Dim errors As Long
Dim warnings As Long
Dim bomType As Long
Dim tableTemplate As String
Dim columnNames(0) As String
Dim columns As Variant
Dim BOMTableName As String
Dim standardViews As Variant
Dim viewIDs(2) As Long
Dim nbrBOMTableAreas As Long</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\advdrawings\bladed shaft.sldasm&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocASSEMBLY, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swModelDocExt = swModel.<b>Extension</b></pre>
<pre>    ' Insert indented BOM table in assembly
    bomType = swBomType_Indented
    tableTemplate = &quot;C:\Program Files\SOLIDWORKS Corp\SOLIDWORKS\lang\english\bom-standard.sldbomtbt&quot;
    Set swBOMAnnotation = swModelDocExt.<b>InsertBomTable3</b>(tableTemplate, 0, 1, bomType, &quot;Default&quot;, False, swNumberingType_Detailed, True)        </pre>
<pre>    ' Get title of second column in BOM table to export to SOLIDWORKS MBD 3D PDF
    Set swTableAnnotation = swBOMAnnotation
    columnNames(0) = swTableAnnotation.<b>GetColumnTitle</b>(1)
    Debug.Print &quot;Title of second column to export to SOLIDWORKS MBD 3D PDF: &quot; &amp; columnNames(0)
    columns = columnNames    </pre>
<pre>    ' Get name of BOM to map to SOLIDWORKS MBD 3D PDF
    Set swBOMFeature = swBOMAnnotation.<b>BomFeature</b>
    BOMTableName = swBOMFeature.<b>Name</b>
    Debug.Print &quot;Name of BOM to map to SOLIDWORKS MBD 3D PDF: &quot; &amp; BOMTableName    </pre>
<pre>    ' Get MBD3PdfData object
    Set swMBDPdfData = swModelDocExt.<b>GetMBD3DPdfData</b>    </pre>
<pre>    ' Set to display&nbsp;SOLIDWORKS&nbsp;MBD&nbsp;3D&nbsp;PDF
    swMBDPdfData.<b>ViewPdfAfterSaving</b> = True      </pre>
<pre>    ' Set path for SOLIDWORKS MBD 3D PDF
    swMBDPdfData.<b>FilePath</b> = &quot;c:\temp\MBDAssembly1.PDF&quot;     </pre>
<pre>    ' Set SOLIDWORKS MBD 3D PDF theme
    swMBDPdfData.<b>ThemeName</b> = &quot;c:\program files\solidworks corp\solidworks\data\themes\simple assembly (a4, landscape)\theme.xml&quot;    </pre>
<pre>    ' Set standard views for SOLIDWORKS MBD 3D PDF
    viewIDs(0) = swStandardViews_e.swFrontView
    viewIDs(1) = swStandardViews_e.swTopView
    viewIDs(2) = swStandardViews_e.swDimetricView
    standardViews = viewIDs
    swMBDPdfData.<b>SetStandardViews</b> (standardViews)    </pre>
<pre>    ' Map BOM and export its second column to a BOM Table Area 
    nbrBOMTableAreas = swMBDPdfData.<b>GetBomAreaCount</b>
    If nbrBOMTableAreas &gt; 0 Then
        swMBDPdfData.<b>SetBomTable</b> 0, BOMTableName, columns
    End If</pre>
<pre>    ' Publish SOLIDWORKS MBD 3D PDF
    swModelDocExt.<b>PublishTo3DPDF</b> swMBDPdfData</pre>
<pre>End Sub</pre>

</body>

</html>
