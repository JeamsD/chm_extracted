<html>

<head>
<title>Attach Files to MBD 3D PDF Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,3D PDF">
<meta name=MS-HKWD content="VBA examples,MBD">
<meta name=MS-HKWD content="VBA examples,SOLIDWORKS MBD">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Attach Files to MBD 3D PDF Example (VBA)</h1>
<p>This example shows how to attach files to a SOLIDWORKS MBD 3D PDF.</p>

<pre>'---------------------------------------------------------------
' Preconditions:
' 1. Verify that the specified SOLIDWORKS MBD 3D PDF theme,
'    files to attach, and <b>C:\temp</b> exist.
' 2. Open <i>public_documents</i><b>\samples\tutorial\api\block.sldprt</b>.
' 3. Open the Immediate window.
'
' Postconditions:
' 1. Gets the MBD3DPdfData object.
' 2. Sets the path and file name for the SOLIDWORKS MBD 3D PDF.
' 3. Sets the theme for the SOLIDWORKS MBD 3D PDF.
' 4. Sets the standard views for the SOLIDWORKS MBD 3D PDF.
' 5. Sets the level of accuracy for lossy compression, whether to
'    apply lossy compression to polygons in the model, and
'    whether to export SOLIDWORKS parts and assemblies with PMI
'    annotations to STEP 242 format.
' 6. Sets the files to attach to SOLIDWORKS MBD 3D PDF.
' 7. Creates the SOLIDWORKS MBD 3D PDF.
' 8. Examine the Immediate window.
' 9. Open <b>C:\temp\block.PDF</b> and click the paper clip (<b>Attachments: 
</b>'    <b>View file attachments</b>) in the left-side panel.
'
' NOTE: Because the part is used elsewhere, do not save
' changes.
'---------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swMBDPdfData As SldWorks.MBD3DPdfData
Dim standardViews As Variant
Dim viewIDs(2) As Long
Dim filesToAttach(1) As String
Dim attachedFiles As Variant
Dim status As Long</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swModelDocExt = swModel.<b>Extension</b>    </pre>
<pre>    'Get MBD3DPdfData object
    Set swMBDPdfData = swModelDocExt.<b>GetMBD3DPdfData</b>    </pre>
<pre>    'Set path and file name for SOLIDWORKS MBD 3D PDF
    swMBDPdfData.<b>FilePath</b> = &quot;C:\temp\block.PDF&quot;    </pre>
<pre>    'Set standard views for SOLIDWORKS MBD 3D PDF
    viewIDs(0) = swStandardViews_e.swFrontView
    viewIDs(1) = swStandardViews_e.swTopView
    viewIDs(2) = swStandardViews_e.swDimetricView
    standardViews = viewIDs
    swMBDPdfData.<b>SetStandardViews</b> (standardViews)    </pre>
<pre>    'Set SOLIDWORKS MBD 3D PDF theme
    swMBDPdfData.<b>ThemeName</b> = &quot;C:\Program Files\SolidWorks Corp\SOLIDWORKS\data\themes\simple part (a4, portrait)\theme.xml&quot;

    'Set the level of accuracy for lossy compression, whether to apply
    'lossy compression to polygons in the model, and whether to export
    'SOLIDWORKS parts and assemblies with PMI annotations to STEP 242
    'format
    swMBDPdfData.<b>Accuracy</b> = sw3DPDFAccuracy_e.swLow
    swMBDPdfData.<b>CompressLossyTessellation</b> = True
    swMBDPdfData.<b>CreateAttachSTEP242</b> = True    </pre>
<pre>    'Set the files to attach
    filesToAttach(0) = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\2012-sm.slddrw&quot;
    filesToAttach(1) = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\2012-sm.sldprt&quot;
    attachedFiles = filesToAttach
    swMBDPdfData.<b>SetAttachments</b> attachedFiles    </pre>
<pre>    'Publish to PDF
    status = swModelDocExt.<b>PublishTo3DPDF</b>(swMBDPdfData)
    Debug.Print (&quot;Status of publishing part to SOLIDWORKS MBD 3D PDF (0 = success): &quot; &amp; status)</pre>
<pre>End Sub</pre>

</body>

</html>
