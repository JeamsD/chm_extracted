<html>

<head>
<title>Select Multiple Splines for Loft Guide Curves Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,features">
<meta name=MS-HKWD content="VBA examples,lofts">
<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Select Multiple Splines for Loft Guide Curves Example (VBA)</h1>
<p>This example shows how to select multiple splines for the guide curves for a loft feature.</p>
<pre>'---------------------------------------------------------------
' Preconditions: Verify that the specified part template exists.
'
' Postconditions:
' 1. Creates a new part.
' 2. Creates a profile sketch.
' 3. Creates a reference plane and another profile sketch on that
'    reference plane.
' 4. Creates two splines for the guide curves.
' 5. Selects the profile sketches.
' 6. Selects the splines and groups them as an object.
' 7. Creates a loft feature.
' 8. Examine the FeatureManager design tree and graphics area.
'---------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swSketchSegment As SldWorks.SketchSegment
Dim swSketchManager As SldWorks.SketchManager
Dim swRefPlane As SldWorks.RefPlane
Dim swFeatureManager As SldWorks.FeatureManager
Dim status As Boolean</pre>
<pre>Sub main()
    Set swApp = Application.SldWorks    </pre>
<pre>    'Create a new part
    Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SOLIDWORKS 2017\templates\Part.prtdot&quot;, 0, 0, 0)    </pre>
<pre>    'Create a profile sketch
    Set swModelDocExt = swModel.<b>Extension</b>
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Front Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    swModel.<b>ClearSelection2</b> True
    Set swSketchManager = swModel.<b>SketchManager</b>
    Set swSketchSegment = swSketchManager.<b>CreateEllipse</b>(0, 0, 0, 7.06113079019074E-02, 0, 0, 0, 3.74944141689373E-02, 0)
    swModel.<b>ClearSelection2</b> True
    swSketchManager.<b>InsertSketch</b> True    </pre>
<pre>    'Create a reference plane and another profile sketch
    'on that reference plane
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Front Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, True, 0, Nothing, 0)
    Set swFeatureManager = swModel.<b>FeatureManager</b>
    Set swRefPlane = swFeatureManager.<b>InsertRefPlane</b>(8, 0.07, 0, 0, 0, 0)
    swModel.<b>ClearSelection2</b> True
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Plane1&quot;, &quot;PLANE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swSketchSegment = swSketchManager.<b>CreateEllipse</b>(0, 0, 0, 5.27205722070845E-02, 0, 0, 0, 1.54164850136235E-02, 0)
    swModel.<b>ClearSelection2</b> True
    swSketchManager.<b>InsertSketch</b> True    </pre>
<pre>    'Create a spline
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Right Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Dim pointArray As Variant
    Dim points() As Double
    ReDim points(0 To 14) As Double
    points(0) = -0.07
    points(1) = 1.54164850136235E-02
    points(2) = 0
    points(3) = -5.31092941649547E-02
    points(4) = 2.80386111480766E-02
    points(5) = 0
    points(6) = -2.96934467839947E-02
    points(7) = 2.29795168190776E-02
    points(8) = 0
    points(9) = -1.12921067380967E-02
    points(10) = 0.026354325474415
    points(11) = 0
    points(12) = 0
    points(13) = 3.74944141689373E-02
    points(14) = 0
    pointArray = points
    Set swSketchSegment = swSketchManager.<b>CreateSpline</b>((pointArray))
    swSketchManager.<b>InsertSketch</b> True  
    swModel.<b>ClearSelection2</b> True    </pre>
<pre>    'Create another spline
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Right Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, False, 0, Nothing, 0)
    ReDim points(0 To 8) As Double
    points(0) = -0.07
    points(1) = -1.54164850136235E-02
    points(2) = 0
    points(3) = -3.07689275649068E-02
    points(4) = -2.33694015292372E-02
    points(5) = 0
    points(6) = 0
    points(7) = -3.74944141689373E-02
    points(8) = 0
    pointArray = points
    Set swSketchSegment = swSketchManager.<b>CreateSpline</b>((pointArray))
    swSketchManager.<b>InsertSketch</b> True  
    swModel.<b>ClearSelection2</b> True</pre>
<pre>    'Select the profile sketches
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCH&quot;, -5.85496337278505E-02, 2.09585732143712E-02, 1, True, 0, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Sketch2&quot;, &quot;SKETCH&quot;, -3.79093739088495E-02, 1.07136192740755E-02, 1, True, 0, Nothing, 0)

    'Select the splines for the guide curves
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Spline1@Sketch3&quot;, &quot;EXTSKETCHSEGMENT&quot;, -6.20659823337474E-03, 3.04187689522769E-02, 2, True, 0, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Spline1@Sketch4&quot;, &quot;EXTSKETCHSEGMENT&quot;, -4.02947949143199E-02, -2.06106896601265E-02, 2, True, 0, Nothing, 0)
    'Group the selected splines as an object
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Unknown&quot;, &quot;SELOBJGROUP&quot;, 0, 0, 0, True, 2, Nothing, 0)

    'Create a loft
    swFeatureManager.<b>InsertProtrusionBlend2</b> False, True, False, 1, 0, 0, 1, 1, True, True, False, 0, 0, 0, True, True, True, 0    </pre>
<pre>End Sub</pre>
<p>&nbsp;</p>

</body>

</html>
