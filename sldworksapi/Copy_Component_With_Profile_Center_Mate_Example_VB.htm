<html>

<head>
<title>Copy Component With Profile Center Mate Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,assemblies">
<meta name=MS-HKWD content="VBA examples,components">
<meta name=MS-HKWD content="VBA examples,mates">
<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Copy Component With Profile Center Mate Example (VBA)</h1>
<p>This example shows how to:</p>
<ul>
	<li><font face="Verdana" style="font-size: 8pt">create a new assembly.</font></li>
	<li><font face="Verdana" style="font-size: 8pt">add two components to the 
	assembly.</font></li>
	<li><font face="Verdana" style="font-size: 8pt">create a profile center mate 
	between the components.</font></li>
	<li><font face="Verdana" style="font-size: 8pt">copy a component with that 
	mate.</font></li>
</ul>

<pre>'--------------------------------------------------------
' Preconditions:
' 1. Open a new SOLIDWORKS session.
' 2. Copy <i>public_documents</i><b>\samples\tutorial\api\block20.sldprt</b> and
'    <b>cylinder20.sldprt</b> to <b>c:\temp</b>.
' 3. Verify that the specified assembly document template exists.
'
' Postconditions:
' 1. Opens both parts.
' 2. Creates a new assembly.
' 3. Inserts both parts into the new assembly.
' 4. Creates a profile center mate between the two components.
' 5. Copies a component and the profile center mate.
' 6. To verify steps 4 and 5:
'    * Examine the graphics area and FeatureManager design tree.
'    * Expand <b>Mates</b> in the FeatureManager design tree.
'---------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swPart1 As SldWorks.PartDoc
Dim swPart2 As SldWorks.PartDoc
Dim swModel As SldWorks.ModelDoc2
Dim swAssemblyDoc As SldWorks.AssemblyDoc
Dim swComponent1 As SldWorks.Component2
Dim swComponent2 As SldWorks.Component2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swMate As SldWorks.Mate2
Dim status As Boolean
Dim errors As Long
Dim warnings As Long
Dim swComponentArray(0) As SldWorks.Component2
Dim repeatArray(0) As Boolean
Dim mateReferencesArray(0) As Object
Dim valueArray(0) As Double
Dim flipAlignmentArray(0) As Boolean
Dim flipDimensionArray(0) As Boolean
Dim lockRotationArray(0) As Boolean
Dim orientationArray(0) As Long</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks    </pre>
<pre>    ' Open parts for new assembly
    Set swPart1 = swApp.<b>OpenDoc6</b>(&quot;C:\temp\block20.sldprt&quot;, swDocumentTypes_e.swDocPART, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swPart2 = swApp.<b>OpenDoc6</b>(&quot;C:\temp\cylinder20.sldprt&quot;, swDocumentTypes_e.swDocPART, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    
    'Open new assembly document
    Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SOLIDWORKS 2016\templates\Assembly.asmdot&quot;, 0, 0, 0)
    swApp.<b>ActivateDoc2</b> &quot;Assem1&quot;, False, errors
    Set swAssemblyDoc = swModel    </pre>
<pre>    ' Add components to assembly document
    Set swComponent1 = swAssemblyDoc.<b>AddComponent5</b>(&quot;C:\temp\block20.sldprt&quot;, swAddComponentConfigOptions_e.swAddComponentConfigOptions_CurrentSelectedConfig, &quot;&quot;, False, &quot;&quot;, 5.22792702800426E-02, 6.58677995643489E-02, 0.102016044707081)
    Set swComponent2 = swAssemblyDoc.<b>AddComponent5</b>(&quot;C:\temp\cylinder20.sldprt&quot;, swAddComponentConfigOptions_e.swAddComponentConfigOptions_CurrentSelectedConfig, &quot;&quot;, False, &quot;&quot;, 0.177061497059185, -1.51244836160913E-03, 6.73233033157885E-02)
    swModel.<b>ViewZoomtofit2</b>    </pre>
<pre>    ' Add profile center mate
    Set swModelDocExt = swModel.<b>Extension</b>
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, 6.31695178495306E-02, 8.56797995642182E-02, 0.137370061843797, True, 1, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;FACE&quot;, 0.141204290267694, 0.031253551638315, 8.43440006535161E-02, True, 1, Nothing, 0)
    Set swMate = swAssemblyDoc.<b>AddMate5</b>(swMateType_e.swMatePROFILECENTER, swMateAlign_e.swMateAlignALIGNED, True, 0.0762, 0.0254, 0.0254, 0.0254, 0.0254, 0, 0.5235987755983, 0.5235987755983, False, True, swMateWidthOptions_e.swMateWidth_Centered, errors)
    swModel.<b>ClearSelection2</b> True
    
    ' Copy component with profile center mate
    Set swComponentArray(0) = swComponent2
    repeatArray(0) = True
    Set mateReferencesArray(0) = Nothing
    valueArray(0) = 0.05
    flipAlignmentArray(0) = True
    flipDimensionArray(0) = True
    lockRotationArray(0) = False
    orientationArray(0) = 0
    status = swAssemblyDoc.<b>CopyWithMates2</b>(swComponentArray, repeatArray, mateReferencesArray, valueArray, flipAlignmentArray, flipDimensionArray, lockRotationArray, orientationArray)</pre>
<pre>End Sub</pre>

</body>

</html>
