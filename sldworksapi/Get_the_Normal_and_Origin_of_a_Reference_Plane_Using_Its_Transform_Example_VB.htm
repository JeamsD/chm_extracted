<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Get the Normal and Origin of a Reference Plane Using Its Transform Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,planes">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=3925>
<meta name=layout-width content=786>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Get the Normal and Origin of a Reference Plane Using Its Transform 
 Example (VBA)</h1>

<p>This example shows how to get the normal and origin of the reference 
 plane using its transform. </p>

<p><b>NOTE:</b> The trick is to know that the reference plane's transform is the transform 
 that takes a reference plane from its canonical position/orientation to 
 its actual position/orientation. In this example, the canonical position 
 is aligned with the system defined <span style="font-weight: bold;">Front 
 Plane</span>.</p>

<pre>'------------------------------------------------------------
' Preconditions: Open a part document that has these planes:
' * Front Plane
' * Top Plane
' * Right Plane
' * Plane2
' * Plane3
' * Plane4
' * Plane5
'
' Postconditions:
' 1. Creates a 3D sketch (i.e., a line) from each reference plane
'    origin in the direction of the reference plane normal.
' 2. Examine the graphics area and FeatureManager design tree.
'-------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks</pre>
<pre>Sub main()</pre>
<pre>    Dim swModel As SldWorks.ModelDoc2
    Dim swPart As SldWorks.PartDoc
    Dim swFeature As SldWorks.Feature
    Dim swRefPlane As SldWorks.RefPlane
    Dim swOriginPoint As SldWorks.MathPoint
    Dim swRefPlaneTransform As SldWorks.MathTransform
    Dim swMathUtility As SldWorks.MathUtility
    Dim aPointData(2) As Double
    Dim vPointData As Variant
    Dim swOriginPointOnRefPlane As SldWorks.MathPoint
    Dim swNormalVector As SldWorks.MathVector
    Dim swNormalVectorRefPlane As SldWorks.MathVector
    Dim aVectorData(2) As Double
    Dim vVectorData  As Variant
    Dim dScaleFactor As Double
    Dim strRefPlaneName As String
    Dim aRefPlanes(6) As String
    Dim lIdx As Long
    
</pre>
<pre>    ' Connect to SOLIDWORKS
    Set swApp = Application.SldWorks</pre>
<pre>    ' Get the math utility
    Set swMathUtility = swApp.<b>GetMathUtility</b></pre>
<pre>    ' Get active document
    Set swModel = swApp.<b>ActiveDoc</b></pre>
<pre>    ' Cast down
    Set swPart = swModel</pre>
<pre>    ' Fill an array with names of reference planes to visit:
    ' *  You can replace this by a loop over all reference planes in the model
    ' *  In particular, check the &quot;Front Plane&quot; as this defines the canonical
    '    position and orientation of a reference plane
    aRefPlanes(0) = &quot;Front Plane&quot;
    aRefPlanes(1) = &quot;Top Plane&quot;
    aRefPlanes(2) = &quot;Right Plane&quot;
    aRefPlanes(3) = &quot;Plane2&quot;
    aRefPlanes(4) = &quot;Plane3&quot;
    aRefPlanes(5) = &quot;Plane4&quot;
    aRefPlanes(6) = &quot;Plane5&quot;</pre>
<pre>    ' Loop over all reference planes of interest
    For lIdx = LBound(aRefPlanes) To UBound(aRefPlanes)
        ' Get reference plane feature by name as feature
        Set swFeature = swPart.<b>FeatureByName</b>(aRefPlanes(lIdx))
        ' Get the name
        strRefPlaneName = swFeature.<b>Name</b>
        ' Cast down
        Set swRefPlane = swFeature.<b>GetSpecificFeature2</b>
        ' Get the reference plane transform:
        ' * This transform takes a reference plane from
        '   its canonical position/orientation to its
        '   actual Position / Orientation
        ' * The canonical position/orientation is aligned
        '   with the system defined &quot;Front Plane&quot;
        Set swRefPlaneTransform = swRefPlane.<b>Transform</b></pre>
<pre>        ' Create a math point that represents the reference plane's origin
        ' in the canonical position in world coordinates; this is
        ' (0.0, 0.0, 0.0)</pre>
<pre>        ' Create array data
        aPointData(0) = 0#
        aPointData(1) = 0#
        aPointData(2) = 0#</pre>
<pre>        ' Turn into a Variant
        vPointData = aPointData</pre>
<pre>        ' Create a math point
        Set swOriginPoint = swMathUtility.<b>CreatePoint</b>(vPointData)</pre>
<pre>        ' Transform the reference plane origin from its canonical
        ' position to its actual position
        Set swOriginPointOnRefPlane = swOriginPoint.<b>MultiplyTransform</b>(swRefPlaneTransform)</pre>
<pre>        ' Get point data
        vPointData = swOriginPointOnRefPlane.<b>ArrayData</b></pre>
<pre>        ' Create a math vector that represents the reference plane's normal in
        ' the canonical orientation in world coordinates; this is [0.0, 0.0, 1.0]</pre>
<pre>        ' Create array data
        aVectorData(0) = 0#
        aVectorData(1) = 0#
        aVectorData(2) = 1#</pre>
<pre>        ' Turn into a Variant
        vVectorData = aVectorData</pre>
<pre>        ' Create a math vector
        Set swNormalVector = swMathUtility.<b>CreateVector</b>(vVectorData)
            
</pre>
<pre>        ' Now transform the reference plane normal from its canonical
        ' orientation to its actual orientation
        Set swNormalVectorRefPlane = swNormalVector.<b>MultiplyTransform</b>(swRefPlaneTransform)</pre>
<pre>        ' Get vector data
        vVectorData = swNormalVectorRefPlane.<b>ArrayData</b></pre>
<pre>        ' Visualize</pre>
<pre>        ' As the reference plane normal is normalized, it may be bit out of
        ' proportion; this factor is arbitrary
        dScaleFactor = 0.1</pre>
<pre>        ' Insert a 3D sketch
        swModel.<b>Insert3DSketch2</b> True</pre>
<pre>        ' Create a line from the reference plane origin in the direction
        ' of the reference plan normal
        swModel.<b>CreateLine2</b> vPointData(0), vPointData(1), vPointData(2), vPointData(0) + dScaleFactor * vVectorData(0), vPointData(1) + dScaleFactor * vVectorData(1), vPointData(2) + dScaleFactor * vVectorData(2)</pre>
<pre>        ' Close the sketch
        swModel.<b>Insert3DSketch2</b> True</pre>
<pre>        ' The sketch is still selected
        Set swFeature = swModel.<b>SelectionManager</b>.<b>GetSelectedObject6</b>(1, 0)
        
</pre>
<pre>        ' Rename it
        swFeature.<b>Name</b> = strRefPlaneName &amp; &quot;-Normal&quot;</pre>
<pre>        ' Clear selection for the next pass
        swModel.<b>ClearSelection2</b> True
    Next lIdx</pre>
<pre>End Sub</pre>

</body>

</html>
