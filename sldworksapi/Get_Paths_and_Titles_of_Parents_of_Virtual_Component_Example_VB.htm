<html>

<head>
<title>Get Paths and Titles of Parents of Virtual Component (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,virtual component">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Get Paths and Titles of Parents of Virtual Component (VBA)</h1>
<p>This example shows how to get the paths and titles of the parent assembly 
components of a virtual component.</p>
<pre>'-----------------------------------------------------------
' Preconditions:
' 1. Open:
'    <i>public_documents</i><b>\samples\tutorial\api\assem2.sldasm</b>
' 2. Open the Immediate window.
'
' Postconditions: The paths and titles of the parent
' assembly components, up to and including the first non-virtual 
' parent assembly component, are printed in the Immediate Window.
'------------------------------------------------------------
Option Explicit</pre>
<pre>Sub main()
    Dim swApp As SldWorks.SldWorks
    Set swApp = Application.SldWorks
    </pre>
<pre>    Dim doc As ModelDoc2
    Set doc = swApp.<b>ActiveDoc</b>
    If doc Is Nothing Then Exit Sub
    If doc.<b>GetType</b> &lt;&gt; swDocASSEMBLY Then Exit Sub
    </pre>
<pre>    Dim asm As AssemblyDoc
    Set asm = doc
    </pre>
<pre>    Dim components As Variant
    components = asm.<b>GetComponents</b>(False)   ' Get all components
    </pre>
<pre>    If IsArray(components) Then
        Debug.Print &quot;Virtual components in this assembly:&quot;
    </pre>
<pre>        Dim vComp As Variant
        For Each vComp In components
            Dim comp As Component2
            Set comp = vComp
            </pre>
<pre>            Dim compDoc As ModelDoc2
            Set compDoc = comp.<b>GetModelDoc2</b>
            If Not compDoc Is Nothing Then
                Dim bResult3 As Boolean
                Dim pathChain As Variant
                Dim titleChain As Variant
                bResult3 = compDoc.Extension.<b>IsVirtualComponent3</b>(pathChain, titleChain)
                    </pre>
<pre>                If bResult3 &lt;&gt; False Then
                    Debug.Print &quot;  Virtual component name: &quot; &amp; comp.Name2
                    </pre>
<pre>                    Debug.Print &quot;    &quot; &amp; &quot;Paths:&quot;
                    Dim vPath As Variant
                    For Each vPath In pathChain
                        Debug.Print &quot;      &quot; &amp; vPath
                    Next
                    </pre>
<pre>                    Debug.Print &quot;    &quot; &amp; &quot;Titles:&quot;
                    Dim vTitle As Variant
                    For Each vTitle In titleChain
                        Debug.Print &quot;      &quot; &amp; vTitle
                    Next
                End If
            Else
                Debug.Print comp.<b>Name2</b> &amp; &quot; &lt;&lt;&lt; NOT LOADED &gt;&gt;&gt;&quot;
            End If
        Next
    End If
    </pre>
<pre>End Sub</pre>

</body>

</html>
