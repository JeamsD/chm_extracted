<html>

<head>
<title>Create Extrude Feature Using Sketch Contours Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,features">
<meta name=MS-HKWD content="VBA examples,contour selections">
<meta name=MS-HKWD content="VBA examples,extrude feature">
<meta name=MS-HKWD content="VBA examples,sketch contours">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Create Extrude Feature Using Sketch Contours Example (VBA)</h1>
<p>This example shows how to create an extrude feature using sketch contours.</p>

<pre>'--------------------------------------------------------------
' Preconditions:
' 1. Verify that the specified part template exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens a new part document.
' 2. Creates a sketch containing sketch contours.
' 3. Creates a boss extrude feature using the sketch of sketch
'    contours.
' 4. Selects the boss extrude feature and accesses
'    its data.
' 5. Gets the sketch contours.
' 6. Get whether each sketch contour is open or closed.
' 7. Examine the FeatureManager design tree, graphics area, and
'    the Immediate window.
'--------------------------------------------------------------</pre>
<pre>Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swSketchMgr As SldWorks.SketchManager
Dim swSketchSegment As SldWorks.SketchSegment
Dim swModelDocExtension As SldWorks.ModelDocExtension
Dim swSelectionMgr As SldWorks.SelectionMgr
Dim swFeatureMgr As SldWorks.FeatureManager
Dim swFeature As SldWorks.Feature
Dim swExtrudeFeatureData As SldWorks.ExtrudeFeatureData2
Dim status As Boolean
Dim skcontours As Variant
Dim skcontour As SldWorks.SketchContour
Dim nbrContours As Long
Dim i As Long</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SolidWorks\SolidWorks 2016\templates\Part.prtdot&quot;, 0, 0, 0)
    Set swModelDocExtension = swModel.<b>Extension</b>
    Set swSketchMgr = swModel.<b>SketchManager</b>
    Set swSelectionMgr = swModel.<b>SelectionManager</b>
    Set swFeatureMgr = swModel.<b>FeatureManager</b>    </pre>
<pre>    'Create sketch containing sketch contours
    swSketchMgr.<b>InsertSketch</b> True
    Set swSketchSegment = swSketchMgr.<b>CreateCircle</b>(0#, 0#, 0#, 0.010564, -0.041843, 0#)
    swModel.<b>ClearSelection2</b> True
    Set swSketchSegment = swSketchMgr.<b>CreateCircle</b>(0.043155, 0#, 0#, 0.048428, -0.01221, 0#)
    swModel.<b>ClearSelection2</b> True
    Set swSketchSegment = swSketchMgr.<b>CreateCircle</b>(-0.043155, 0#, 0#, -0.043214, -0.014954, 0#)
    swModel.<b>ClearSelection2</b> True
    swSketchMgr.<b>InsertSketch</b> True</pre>
<pre>    'Create boss extrude feature from sketch of sketch contours
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCH&quot;, 0, 0, 0, False, 0, Nothing, 0)
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCHCONTOUR&quot;, -0.047096875714166, 7.24922083273226E-03, 0.018531938896921, True, 0, Nothing, 0)
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCHCONTOUR&quot;, 4.73122625955432E-02, -0.015948285832011, -1.55264330079864E-02, True, 0, Nothing, 0)
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCHCONTOUR&quot;, -8.80361157802517E-03, -2.46473508312897E-02, 1.99951653548178E-02, True, 0, Nothing, 0)
    swModel.<b>ClearSelection2</b> True
    swSelectionMgr.<b>EnableContourSelection</b> = True
<b>    </b>status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCH&quot;, 0, 0, 0, False, 4, Nothing, 0)
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCHCONTOUR&quot;, -0.047096875714166, 7.24922083273226E-03, 0.018531938896921, True, 4, Nothing, 0)
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCHCONTOUR&quot;, 4.73122625955432E-02, -0.015948285832011, -1.55264330079864E-02, True, 4, Nothing, 0)
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Sketch1&quot;, &quot;SKETCHCONTOUR&quot;, -8.80361157802517E-03, -2.46473508312897E-02, 1.99951653548178E-02, True, 4, Nothing, 0)
    Set swFeature = swFeatureMgr.<b>FeatureExtrusion3</b>(True, False, False, 0, 0, 0.01016, 0.00254, False, False, False, False, 1.74532925199433E-02, 1.74532925199433E-02, False, False, False, False, True, True, True, 0, 0, False)
    swSelectionMgr.<b>EnableContourSelection</b> = False    </pre>
<pre>    'Select the boss extrude feature
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Boss-Extrude1&quot;, &quot;BODYFEATURE&quot;, 0, 0, 0, False, 0, Nothing, swSelectOptionDefault)
    Set swFeature = swSelectionMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swExtrudeFeatureData = swFeature.<b>GetDefinition</b>    </pre>
<pre>    'Access the boss extrude feature data
    swExtrudeFeatureData.<b>AccessSelections</b> swModel, Nothing    </pre>
<pre>    'Get the number of sketch contours in the extrude feature
    nbrContours = swExtrudeFeatureData.<b>GetContoursCount</b>
    Debug.Print &quot;Number of sketch contours in the extrude feature: &quot; &amp; nbrContours    </pre>
<pre>    'Get the sketch contours in the extrude feature
    skcontours = swExtrudeFeatureData.<b>contours</b></pre>
<pre>    'Get each sketch contour and whether it is open or closed
    For i = 0 To (nbrContours - 1)
            Set skcontour = skcontours(i)
            Debug.Print &quot;  Sketch contour &quot; &amp; i &amp; &quot; is closed? &quot; &amp; skcontour.<b>IsClosed</b>
    Next i</pre>
<pre>    'Release&nbsp;selection&nbsp;access<span style="color: green">
    </span>swExtrudeFeatureData.<b>ReleaseSelectionAccess</b></pre>
<pre>End Sub</pre>

</body>

</html>
