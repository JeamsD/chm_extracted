<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Check Interference using AssemblyDoc::ToolsCheckInterference2 Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2535>
<meta name=layout-width content=723>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">

 <META NAME="MS-HKWD" CONTENT="VBA examples,interference detection">


</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Check Interference using IAssemblyDoc::ToolsCheckInterference2 Example 
 (VBA)</h1>

<p>This example shows how to check an assembly for interference using IAssemblyDoc::ToolsCheckInterference2.</p>
<pre>'----------------------------------------------------------
' Preconditions:
' 1. Open a fully resolved assembly that contains at least
'    two interfering components.
' 2. Select at least two interfering components.
' 3. Open the Immediate window.
'
' Postconditions:
' 1. Selects the interfering faces.
' 2. Examine the graphics area and Immediate window, then
'    press F5.
' 3. Selects the interfering components.
' 4. Examine the graphics area and Immediate window, then
'    press F5.
'----------------------------------------------------------
Option Explicit</pre>
<pre>Sub main()</pre>
<pre>    Const bCoincidentInterference   As Boolean = False</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swAssy As SldWorks.AssemblyDoc
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim CompArray() As SldWorks.Component2
    Dim swSelData As SldWorks.SelectData
    Dim vCompArray As Variant
    Dim vIntCompArray As Variant
    Dim vIntFaceArray As Variant
    Dim swFace As SldWorks.Face2
    Dim swEnt As SldWorks.Entity
    Dim swComp As SldWorks.Component2
    Dim i As Long
    Dim nSelCount As Long
    Dim bRet As Boolean</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swAssy = swModel
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swSelData = swSelMgr.<b>CreateSelectData</b></pre>
<pre>    Debug.Print &quot;File = &quot; &amp; swModel.<b>GetPathName</b></pre>
<pre>    nSelCount = swSelMgr.<b>GetSelectedObjectCount</b>
    ReDim CompArray(nSelCount - 1)
    Debug.Print &quot;Number of selected components: &quot; &amp; nSelCount
    For i = 0 To (nSelCount - 1)
        Set CompArray(i) = swSelMgr.<b>GetSelectedObjectsComponent2</b>(i + 1)
        Debug.Print &quot;  Comp[&quot; &amp; i &amp; &quot;] = &quot; &amp; CompArray(i).<b>Name2</b>
    Next i
    vCompArray = CompArray
    swAssy.<b>ToolsCheckInterference2</b> nSelCount, (vCompArray), bCoincidentInterference, vIntCompArray, vIntFaceArray
    If (IsEmpty(vIntCompArray) = True) And (IsEmpty(vIntFaceArray) = True) Then
        Debug.Print &quot;  No contact&quot;
        Exit Sub
    End If</pre>
<pre>If Not IsEmpty(vIntFaceArray) Then
        Debug.Print &quot;    &quot; &amp; UBound(vIntFaceArray) + 1 &amp; &quot; faces interfere!&quot;
        swModel.<b>ClearSelection2</b> True
        For i = 0 To UBound(vIntFaceArray)
            Set swFace = vIntFaceArray(i)
            Set swEnt = swFace
            Set swComp = swEnt.<b>GetComponent</b>
            Debug.Print &quot;      Component face[&quot; &amp; i &amp; &quot;] = &quot; &amp; swComp.<b>Name2</b>
            bRet = swEnt.<b>Select4</b>(True, swSelData): Debug.Assert bRet
        Next i
        ' Interfering faces selected        </pre>
<pre>        Stop
        ' Examine the graphics area and Immediate window, then
        ' press F5 to continue</pre>
<pre>    Else
        Debug.Assert Not IsEmpty(vIntCompArray)
        Debug.Assert False = bCoincidentInterference
        Debug.Print &quot;  Faces touch but not checking for coincident interference!&quot;
    End If</pre>
<pre>    If Not IsEmpty(vIntCompArray) Then
        Debug.Print &quot;  &quot; &amp; UBound(vIntCompArray) + 1 &amp; &quot; Components interfere!&quot;
        swModel.<b>ClearSelection2</b> True
        For i = 0 To UBound(vIntCompArray)
            Set swComp = vIntCompArray(i)
            Debug.Print &quot;    Component [&quot; &amp; i &amp; &quot;] = &quot; &amp; swComp.<b>Name2</b>
            bRet = swComp.<b>Select2</b>(True, 0): Debug.Assert bRet
        Next i</pre>
<pre>        ' Interfering components selected        </pre>
<pre>        Stop
        ' Examine the graphics area and Immediate window, then
        ' press F5 to continue
        </pre>
<pre>    End If</pre>
<pre>End Sub</pre>

</body>

</html>
