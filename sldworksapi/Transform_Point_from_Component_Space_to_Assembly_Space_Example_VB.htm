<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Transform Point from Component Space to Assembly Space Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=1157>
<meta name=layout-width content=786>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">
 <META NAME="MS-HKWD" CONTENT="VBA examples,transforms">
 <META NAME="MS-HKWD" CONTENT="VBA examples,component space">
 <META NAME="MS-HKWD" CONTENT="VBA examples,assembly space">
</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Transform Point from Component Space to Assembly Space Example (VBA)</h1>

<p>This example shows how to transform a point from component space to 
 assembly space.</p>
<pre>'--------------------------------------------------------------
' Preconditions:
' 1. Verify that the specified assembly document to open
'    exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the specified assembly document.
' 2. Selects a component.
' 3. Transforms the component's origin to a point in
'    assembly space.
' 4. Examine the Immediate window.
'--------------------------------------------------------------</pre>
<pre>Option Explicit</pre>
<pre>Sub main()</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swMathUtil As SldWorks.MathUtility
    Dim swModel As SldWorks.ModelDoc2
    Dim swModelDocExt As SldWorks.ModelDocExtension
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swComp As SldWorks.Component2
    Dim swXform As SldWorks.MathTransform
    Dim nPt(2) As Double
    Dim vPt As Variant
    Dim swPt As SldWorks.MathPoint
    Dim bRet As Boolean
    Dim errors As Long
    Dim warnings As Long
    Dim fileName As String</pre>
<pre>    Set swApp = Application.SldWorks    </pre>
<pre>    ' Open assembly
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\smartcomponents\stepped_shaft.sldasm&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocASSEMBLY, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swModelDocExt = swModel.<b>Extension</b>
    bRet = swModelDocExt.<b>SelectByID2</b>(&quot;stepped_shaft-1@stepped_shaft&quot;, &quot;COMPONENT&quot;, 0, 0, 0, False, 0, Nothing, 0)    </pre>
<pre>    Set swMathUtil = swApp.<b>GetMathUtility</b>
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swComp = swSelMgr.<b>GetSelectedObjectsComponent</b>(1)
    Set swXform = swComp.<b>Transform2</b></pre>
<pre>    ' Point at component origin
    nPt(0) = 0#
    nPt(1) = 0#
    nPt(2) = 0#
    vPt = nPt
    Set swPt = swMathUtil.<b>CreatePoint</b>(vPt)
    Set swPt = swPt.<b>MultiplyTransform</b>(swXform)</pre>
<pre>    Debug.Print &quot;File = &quot; &amp; swModel.<b>GetPathName</b>
    Debug.Print &quot;  Component = &quot; &amp; swComp.<b>Name2</b> &amp; &quot; [&quot; &amp; swComp.<b>GetPathName</b> &amp; &quot;]&quot;
    Debug.Print &quot;    Point in component = (&quot; &amp; nPt(0) * 1000# &amp; &quot;, &quot; &amp; nPt(1) * 1000# &amp; &quot;, &quot; &amp; nPt(2) * 1000# &amp; &quot;) mm&quot;
    Debug.Print &quot;    Point in assembly = (&quot; &amp; swPt.<b>ArrayData</b>(0) * 1000# &amp; &quot;, &quot; &amp; swPt.<b>ArrayData</b>(1) * 1000# &amp; &quot;, &quot; &amp; swPt.<b>ArrayData</b>(2) * 1000# &amp; &quot;) mm&quot;
    </pre>
<pre>End Sub</pre>

</body>

</html>
