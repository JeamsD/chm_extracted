<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Get Assembly Bounding Box Using Components Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,bounding box">
<meta name=MS-HKWD content="VBA examples,database">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=3791>
<meta name=layout-width content=879>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./ApiHelp.css">



</head>

<!--(Body)==========================================================-->


<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>
<h1>Get Assembly Bounding Box Using Components Example (VBA)</h1>

<p>This example shows how to get a bounding box for an assembly.</p>

<pre>'--------------------------------------------
' Preconditions:
' 1. Open an assembly document.
' 2. Ensure that all components in the assembly are fully resolved.
' 3. Open the Immediate window.
' 4. Run the macro.
'
' Postconditions:
' 1. Adds a 3D sketch to the assembly showing the bounding box.
' 2. Examine the graphics area and Immediate window to verify.
'
' <b>NOTES</b>:
' * Because all assembly component bounding boxes are
'   approximated, the bounding box for the assembly
'   is also approximated.
' * Resulting bounding box is oriented with the assembly
'   coordinate system.
'----------------------------------------------
 
</pre>
<pre>Option Explicit
</pre>
<pre>Function GetMax(ByVal Val1 As Double, ByVal Val2 As Double, ByVal Val3 As Double) As Double
' Finds maximum of 3 values
    GetMax = Val1
    If Val2 &gt; GetMax Then
        GetMax = Val2
    End If
    If Val3 &gt; GetMax Then
        GetMax = Val3
    End If
End Function</pre>
<pre>Function GetMin(ByVal Val1 As Double, ByVal Val2 As Double, ByVal Val3 As Double) As Double</pre>
<pre>' Finds minimum of 3 values
    GetMin = Val1
    If Val2 &lt; GetMin Then
        GetMin = Val2
    End If
    If Val3 &lt; GetMin Then
        GetMin = Val3
    End If
End Function</pre>
<pre>Sub main()</pre>
<pre>    Const MaxDouble             As Double = 1.79769313486231E+308
    Const MinDouble             As Double = -1.79769313486231E+308
    Dim swApp                   As SldWorks.SldWorks
    Dim swModel                 As SldWorks.ModelDoc2
    Dim swAssy                  As SldWorks.AssemblyDoc
    Dim swConfigurationMgr      As SldWorks.ConfigurationManager
    Dim swConfig                As SldWorks.Configuration
    Dim swRootComp              As SldWorks.Component2
    Dim vChild                  As Variant
    Dim swChildComp             As SldWorks.Component2
    Dim vBox                    As Variant
    Dim X_max                   As Double
    Dim X_min                   As Double
    Dim Y_max                   As Double
    Dim Y_min                   As Double
    Dim Z_max                   As Double
    Dim Z_min                   As Double
    Dim swSketchMgr             As SldWorks.SketchManager
    Dim swSketchPt(8)           As SldWorks.SketchPoint
    Dim swSketchSeg(12)         As SldWorks.SketchSegment
    Dim i                       As Long
</pre>
<pre>    Set swApp = CreateObject(&quot;SldWorks.Application&quot;)
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swAssy = swModel
    Set swConfigurationMgr = swModel.<b>ConfigurationManager</b>
    Set swConfig = swConfigurationMgr.<b>ActiveConfiguration</b>
    Set swRootComp = swConfig.<b>GetRootComponent3</b>(True)
    
</pre>
<pre>    ' Initialize values
    X_max = MinDouble
    X_min = MaxDouble
    Y_max = MinDouble
    Y_min = MaxDouble
    Z_max = MinDouble
    Z_min = MaxDouble</pre>
<pre>    vChild = swRootComp.<b>GetChildren</b>
    For i = 0 To UBound(vChild)
        Set swChildComp = vChild(i)</pre>
<pre>        If swChildComp.<b>Visible</b> = swComponentVisible Then
            vBox = swChildComp.<b>GetBox</b>(False, False)
            X_max = GetMax(vBox(0), vBox(3), X_max)
            X_min = GetMin(vBox(0), vBox(3), X_min)
            Y_max = GetMax(vBox(1), vBox(4), Y_max)
            Y_min = GetMin(vBox(1), vBox(4), Y_min)
            Z_max = GetMax(vBox(2), vBox(5), Z_max)
            Z_min = GetMin(vBox(2), vBox(5), Z_min)
        End If
    Next i</pre>
<pre>    Debug.Print &quot;Assembly Bounding Box (&quot; + swModel.<b>GetPathName</b> + &quot;) = &quot;
    Debug.Print &quot;  (&quot; + Str(X_min * 1000#) + &quot;,&quot; + Str(Y_min * 1000#) + &quot;,&quot; + Str(Z_min * 1000#) + &quot;) mm&quot;
    Debug.Print &quot;  (&quot; + Str(X_max * 1000#) + &quot;,&quot; + Str(Y_max * 1000#) + &quot;,&quot; + Str(Z_max * 1000#) + &quot;) mm&quot;</pre>
<pre>    Set swSketchMgr = swModel.<b>SketchManager</b></pre>
<pre>    swSketchMgr.<b>Insert3DSketch</b> True
    swSketchMgr.<b>AddToDB</b> = True
    </pre>
<pre>    ' Draw points at each corner of bounding box
    Set swSketchPt(0) = swSketchMgr.<b>CreatePoint</b>(X_min, Y_min, Z_min)
    Set swSketchPt(1) = swSketchMgr.<b>CreatePoint</b>(X_min, Y_min, Z_max)
    Set swSketchPt(2) = swSketchMgr.<b>CreatePoint</b>(X_min, Y_max, Z_min)
    Set swSketchPt(3) = swSketchMgr.<b>CreatePoint</b>(X_min, Y_max, Z_max)
    Set swSketchPt(4) = swSketchMgr.<b>CreatePoint</b>(X_max, Y_min, Z_min)
    Set swSketchPt(5) = swSketchMgr.<b>CreatePoint</b>(X_max, Y_min, Z_max)
    Set swSketchPt(6) = swSketchMgr.<b>CreatePoint</b>(X_max, Y_max, Z_min)
    Set swSketchPt(7) = swSketchMgr.<b>CreatePoint</b>(X_max, Y_max, Z_max)</pre>
<pre>    ' Draw bounding box
    Set swSketchSeg(0) = swSketchMgr.<b>CreateLine</b>(X_min, Y_min, Z_min, X_max, Y_min, Z_min)
    Set swSketchSeg(1) = swSketchMgr.<b>CreateLine</b>(X_max, Y_min, Z_min, X_max, Y_min, Z_max)
    Set swSketchSeg(2) = swSketchMgr.<b>CreateLine</b>(X_max, Y_min, Z_max, X_min, Y_min, Z_max)
    Set swSketchSeg(3) = swSketchMgr.<b>CreateLine</b>(X_min, Y_min, Z_max, X_min, Y_min, Z_min)
    Set swSketchSeg(4) = swSketchMgr.<b>CreateLine</b>(X_min, Y_min, Z_min, X_min, Y_max, Z_min)
    Set swSketchSeg(5) = swSketchMgr.<b>CreateLine</b>(X_min, Y_min, Z_max, X_min, Y_max, Z_max)
    Set swSketchSeg(6) = swSketchMgr.<b>CreateLine</b>(X_max, Y_min, Z_min, X_max, Y_max, Z_min)
    Set swSketchSeg(7) = swSketchMgr.<b>CreateLine</b>(X_max, Y_min, Z_max, X_max, Y_max, Z_max)
    Set swSketchSeg(8) = swSketchMgr.<b>CreateLine</b>(X_min, Y_max, Z_min, X_max, Y_max, Z_min)
    Set swSketchSeg(9) = swSketchMgr.<b>CreateLine</b>(X_max, Y_max, Z_min, X_max, Y_max, Z_max)
    Set swSketchSeg(10) = swSketchMgr.<b>CreateLine</b>(X_max, Y_max, Z_max, X_min, Y_max, Z_max)
    Set swSketchSeg(11) = swSketchMgr.<b>CreateLine</b>(X_min, Y_max, Z_max, X_min, Y_max, Z_min)</pre>
<pre>    swSketchMgr.<b>AddToDB</b> = False
    swSketchMgr.<b>Insert3DSketch</b> True</pre>
<pre>End Sub</pre>

</body>

</html>
