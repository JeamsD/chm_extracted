<html>

<head>
<title>Get Display State Names and Visibilities of Components Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,display states">
<meta name=MS-HKWD content="VBA examples,components">
<meta name=MS-HKWD content="VBA examples,assemblies">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Get Display State Names and Visibilities of Components Example (VBA)</h1>
<p>This example shows how to get the na<font face="Verdana" style="font-size: 8pt">mes 
of the display states in an assembly and the visibilities of the assembly 
components in each display state.</font></p>
<pre>'------------------------------------------------------------------------------
' Preconditions:
' 1. Open an assembly that contains multiple components
'    and display states.
' 2. Open the Immediate window.
'
' Postconditions: Examine the Immediate window.
'-----------------------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swConfigMgr As SldWorks.ConfigurationManager
Dim swConfig As SldWorks.Configuration
Dim components As Variant
Dim docType As Long
Dim i As Long
Dim j As Long
Dim displayStateNames As Variant
Dim displayStateName As String
Dim displayStateVisibilities As Variant
Dim visibility As String</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swModelDocExt = swModel.<b>Extension</b></pre>
<pre>    docType = swModel.<b>GetType</b>
    If docType = swDocumentTypes_e.swDocASSEMBLY Then    </pre>
<pre>        Set swConfigMgr = swModel.<b>ConfigurationManager</b>
        Set swConfig = swConfigMgr.<b>ActiveConfiguration</b>
        displayStateNames = swConfig.<b>GetDisplayStates</b>
        For i = 0 To UBound(displayStateNames)
            displayStateName = displayStateNames(i)
            Debug.Print &quot;Display state name: &quot; &amp; displayStateName
            displayStateVisibilities = swConfig.<b>GetDisplayStateComponentVisibility</b>(displayStateName, False, False, components)
            Debug.Print (&quot;  Display state visibility: &quot;)
            For j = 0 To UBound(displayStateVisibilities)
                Select Case displayStateVisibilities(j)
                    Case 0
                        visibility = &quot;Hidden&quot;
                    Case 1
                        visibility = &quot;Shown&quot;
                End Select
                Debug.Print (&quot;    &quot; &amp; components(j).<b>Name2</b> &amp; &quot;: &quot; &amp; visibility)
            Next j
        Next i
    Else
        Debug.Print &quot;Open an assembly document with multiple display states.&quot;
        Exit Sub
    End If    </pre>
<pre>End Sub</pre>

</body>

</html>
