<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Set Fully Resolved Assembly to Lightweight Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,assemblies">
<meta name=MS-HKWD content="VBA examples,components">
<meta name=MS-HKWD content="VBA examples,lightweight">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2255>
<meta name=layout-width content=786>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Set Fully Resolved Assembly to Lightweight Example (VBA)</h1>

<p>This example shows how to set a fully resolved assembly to lightweight.</p>
<pre>'----------------------------------------------------------------
' Preconditions:
' 1. Open a fully resolved assembly document.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Sets all assembly components to lightweight.
' 2. Examine the Immediate window and FeatureManager design tree.
'----------------------------------------------------------------
</pre>
<pre>Sub SetComponentLightWeight(sPadStr As String, swComp As SldWorks.Component2)</pre>
<pre>    Dim vChildArr As Variant
    Dim swChildComp As SldWorks.Component2
    Dim swChildModel As SldWorks.ModelDoc2
    Dim nRetVal As Long
    Dim nDocType As Long
    Dim i As Long
    
</pre>
<pre>    Debug.Print sPadStr &amp; swComp.<b>Name2</b> &amp; &quot; [&quot; &amp; CBool(swComp.<b>Visible</b>) &amp; &quot;]&quot;</pre>
<pre>    vChildArr = swComp.<b>GetChildren</b>
    For i = 0 To UBound(vChildArr)
        Set swChildComp = vChildArr(i)
        ' Is Nothing if another instance has been previously set to lightweight
        Set swChildModel = swChildComp.<b>GetModelDoc2</b>
        If Not swChildModel Is Nothing Then
            nDocType = swChildModel.<b>GetType</b>
        Else
            nDocType = swDocNONE
        End If</pre>
<pre>        nRetVal = swChildComp.<b>SetSuppression2</b>(swComponentLightweight)
        If swDocPART = nDocType Or swDocNONE = nDocType Then
            Debug.Assert swResolveNotPerformed = nRetVal
        Else
            ' Cannot set a sub-assembly to lightweight; must set each part to lightweight
            Debug.Assert swDocASSEMBLY = swChildModel.<b>GetType</b>
            Debug.Assert swResolveError = nRetVal
        End If</pre>
<pre>        ' Recurse into this component
        SetComponentLightWeight sPadStr &amp; &quot;  &quot;, swChildComp        </pre>
<pre>    Next i</pre>
<pre>End Sub</pre>
<pre>Sub main()</pre>
<pre>    Dim swApp                       As SldWorks.SldWorks
    Dim swModel                     As SldWorks.ModelDoc2
    Dim swAssy                      As SldWorks.AssemblyDoc
    Dim swConfig                    As SldWorks.Configuration
    Dim swConfigMgr                 As SldWorks.ConfigurationManager
    Dim swRootComp                  As SldWorks.Component2
    Dim bRet                        As Boolean</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swAssy = swModel
    Set swConfigMgr = swModel.<b>ConfigurationManager</b>
    Set swConfig = swConfigMgr.<b>ActiveConfiguration</b>
    Set swRootComp = swConfig.<b>GetRootComponent3</b>(True)
    Debug.Print &quot;File = &quot; &amp; swModel.<b>GetPathName</b>
    SetComponentLightWeight &quot;  &quot;, swRootComp</pre>
<pre>    ' Update in-context features
    bRet = swModel.<b>ForceRebuild3</b>(False): Debug.Assert bRet</pre>
<pre>End Sub</pre>

</body>

</html>
