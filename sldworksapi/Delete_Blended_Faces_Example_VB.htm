<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Delete Blended Faces Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta NAME=MS-HKWD CONTENT="VBA examples,faces">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2915>
<meta name=layout-width content=931>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>

<h1>Delete Blended Faces Example (VBA)</h1>

<p>This example shows how to delete blended faces. </p>

<p><span style="font-weight: bold;">NOTE:</span> <!--kadov_tag{{<spaces>}}-->&nbsp;<!--kadov_tag{{</spaces>}}-->You 
 can only delete blended faces from a temporary body.</p>

<pre>'--------------------------------------------------
' Preconditions:
' 1. Open a part that contains one solid body
'    and at least one blended (fillet) face.
' 2. Select a blended face.
'
' Postconditions:
' 1. Creates a new part, which has the same body as
'    the original part, but the selected blended
'    face is removed.
' 2. Examine the graphics area.
'
' NOTE: It might not be possible to remove the
' selected blended face. If it's not removed, then
' the new body is the same as the original
' body.
'--------------------------------------------------
Option Explicit</pre>
<pre>Function GetFacesWithAttribute(swApp As SldWorks.SldWorks, swBody As SldWorks.Body2, swAttDef As SldWorks.AttributeDef) As Variant
    Dim swFace As SldWorks.Face2
    Dim swEnt As SldWorks.Entity
    Dim swAttCopy As SldWorks.Attribute
    Dim swFaceArr() As SldWorks.Face2</pre>
<pre>    ' Search for faces on temporary body based
    ' on copied attributes
    ReDim swFaceArr(0)
    Set swFace = swBody.<b>GetFirstFace</b>
    Do While Not Nothing Is swFace
        Set swEnt = swFace
        Set swAttCopy = Nothing
        ' Only one instance of attribute should exist
        Set swAttCopy = swEnt.<b>FindAttribute</b>(swAttDef, 0)
        If Not swAttCopy Is Nothing Then
            Set swFaceArr(UBound(swFaceArr)) = swFace
            ReDim Preserve swFaceArr(UBound(swFaceArr) + 1)
        End If
        Set swFace = swFace.<b>GetNextFace</b>
    Loop
    ReDim Preserve swFaceArr(UBound(swFaceArr) - 1)
    GetFacesWithAttribute = swFaceArr
End Function</pre>
<pre>Sub main()</pre>
<pre>    '1 = Invisible
    '0 = Visible
    Const CreateVisible         As Long = 0</pre>
<pre>    Const sAttDefName As String = &quot;temp_attrib&quot;
    Const sAttRootName As String = &quot;temp&quot;</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swAttDef As SldWorks.AttributeDef
    Dim swModel As SldWorks.ModelDoc2
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swPart As SldWorks.PartDoc
    Dim nSelCount As Long
    Dim swFace As SldWorks.Face2
    Dim swEnt As SldWorks.Entity
    Dim swAtt() As SldWorks.Attribute
    Dim vFaceArr As Variant
    Dim swFeat As SldWorks.Feature
    Dim vBodies As Variant
    Dim swBody As SldWorks.Body2
    Dim swCopyBody As SldWorks.Body2
    Dim swNewPart As SldWorks.PartDoc
    Dim i As Long
    Dim bRet As Boolean</pre>
<pre>    Set swApp = Application.SldWorks
    Set swAttDef = swApp.<b>DefineAttribute</b>(sAttDefName)
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swPart = swModel</pre>
<pre>    bRet = swAttDef.<b>Register</b></pre>
<pre>    ' Add attribute to selected faces
    nSelCount = swSelMgr.<b>GetSelectedObjectCount</b>
    ReDim swAtt(nSelCount)
    For i = 1 To nSelCount
        Set swFace = swSelMgr.<b>GetSelectedObject6</b>(i, -1)
        Set swEnt = swFace
        Set swAtt(i - 1) = swAttDef.<b>CreateInstance5</b>(swModel, swEnt, sAttRootName &amp; i, CreateVisible, swAllConfiguration)
    Next i</pre>
<pre>    vBodies = swPart.<b>GetBodies2</b>(swAllBodies, False)
    Set swBody = vBodies(0)
    Set swCopyBody = swBody.<b>Copy</b></pre>
<pre>    ' Remove attribute from faces
    For i = 1 To nSelCount
       bRet = swAtt(i - 1).<b>Delete</b>(True)
    Next i</pre>
<pre>    vFaceArr = GetFacesWithAttribute(swApp, swCopyBody, swAttDef)

    ' Can only delete blends from a temporary body
    Debug.Assert swCopyBody.<b>IsTemporaryBody</b></pre>
<pre>    bRet = swCopyBody.<b>DeleteBlends3</b>(vFaceArr, True, True)</pre>
<pre>    Set swNewPart = swApp.<b>NewPart</b>
    Set swFeat = swNewPart.<b>CreateFeatureFromBody3</b>(swCopyBody, False, swCreateFeatureBodyCheck)</pre>
<pre>End Sub</pre>

</body>

</html>
