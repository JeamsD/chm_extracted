<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Roll Back Model Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,rollback">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2869>
<meta name=layout-width content=786>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Roll Back Model Example (VBA)</h1>

<p>This example shows how to step through the FeatureManager design tree 
 of a model by rolling back to each feature in reverse sequence. Running 
 an example like this can provide insight into the design intent of the 
 user.</p>

<pre>'-----------------------------------
' Preconditions:
' 1. Open a part or assembly document.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Observe the FeatureManager design tree
'    while the macro executes.
' 2. Examine the Immediate window to see the
'    names of the features rolled back and forward.
'
' NOTE: The delay between steps is set to 1 second.
'-----------------------------------</pre>
<pre>Option Explicit</pre>
<pre>Sub Main()</pre>
<pre>    ' Delay in seconds
    Const DELAY                     As Single = 1#    </pre>
<pre>    Dim swApp                       As SldWorks.SldWorks
    Dim swModel                     As SldWorks.ModelDoc2
    Dim swModelView                 As SldWorks.ModelView
    Dim swPart                      As SldWorks.PartDoc
    Dim swAssy                      As SldWorks.AssemblyDoc
    Dim swFeatMgr                   As SldWorks.FeatureManager
    Dim swFeat                      As SldWorks.Feature
    Dim featFace                    As Variant
    Dim swFace                      As SldWorks.Face2
    Dim rect                        As Variant
    Dim featName()                  As String
    Dim timeNow                     As Single
    Dim docType                     As Long
    Dim i                           As Long
    Dim j                           As Long
    Dim status                      As Boolean</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swModelView = swModel.<b>ActiveView</b>
    Set rect = Nothing
    Set swFeatMgr = swModel.<b>FeatureManager</b>
    Set swFeat = swModel.<b>FirstFeature</b></pre>
<pre>    docType = swModel.<b>GetType</b>
    Select Case docType
        Case swDocPART
            Set swPart = swModel
        Case swDocASSEMBLY
            Set swAssy = swModel
        Case Else
            Debug.Print &quot;Open a part or assembly document, then rerun this macro.&quot;
            Exit Sub
    End Select</pre>
<pre>    ReDim featName(0)</pre>
<pre>    Do While Not swFeat Is Nothing
        featName(UBound(featName)) = swFeat.<b>Name</b>
        ReDim Preserve featName(UBound(featName) + 1)
        Set swFeat = swFeat.<b>GetNextFeature</b>
    Loop</pre>
<pre>    ' Loop over-allocates array by 1, so remove last (empty) entry
    ReDim Preserve featName(UBound(featName) - 1)</pre>
<pre>    ' Playback each feature in the FeatureManager design tree
    For i = 0 To UBound(featName)
        Debug.Print featName(i)
        status = swFeatMgr.<b>EditRollback</b>(swMoveRollbackBarToAfterFeature, featName(i))</pre>
<pre>        ' Do not assert because you may be trying to roll back or roll forward
        ' to a feature that cannot be rolled back or forward to; for example,
        ' the Lighting or Annotations folder        </pre>
<pre>        'Debug.Assert status
        
</pre>
<pre>        ' Remove any previous highlights
        swModelView.<b>GraphicsRedraw</b> (rect)</pre>
<pre>        ' Highlight feature if it has any geometry
        Select Case docType
            Case swDocPART
                Set swFeat = swPart.<b>FeatureByName</b>(featName(i))
            Case swDocASSEMBLY
                Set swFeat = swAssy.<b>FeatureByName</b>(featName(i))
        End Select</pre>
<pre>        featFace = swFeat.<b>GetFaces</b>
        If Not IsEmpty(featFace) Then
            For j = 0 To UBound(featFace)
                Set swFace = featFace(j)
                swFace.<b>Highlight</b> True
            Next j
        End If</pre>
<pre>        ' Only pause if rollback is successful
        If status Then
            timeNow = Timer
            While timeNow + DELAY &gt; Timer
                ' Allow SOLIDWORKS to refresh screen
                DoEvents
            Wend
        End If
    Next i</pre>
<pre>    ' Remove highlight from last feature
    swModelView.<b>GraphicsRedraw</b> (rect)</pre>
<pre>End Sub</pre>

</body>

</html>
