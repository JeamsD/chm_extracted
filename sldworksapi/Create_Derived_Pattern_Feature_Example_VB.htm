<html>

<head>
<title>Create Derived Pattern Feature Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,patterns">
<meta name=MS-HKWD content="VBA examples,derived patterns">
<meta name=MS-HKWD content="VBA examples,features">
<meta name=MS-HKWD content="VBA examples,pattern driven pattern features">
<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Create Derived Pattern Feature Example (VBA)</h1>
<p>This example shows how to create a derived pattern feature.</p>

<pre>'--------------------------------------------------------------------
' Preconditions:
' 1. Verify that the assembly to open exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the assembly.
' 2. Creates a derived pattern feature.
' 3. Gets the type and name of the pattern feature used for
'    the derived pattern feature.
' 4. Changes the position of the pattern instance used as the seed
'    feature in the derived pattern feature.
' 5. Examine the FeatureManager design tree, Immediate window, and
'    graphics area.
'
' NOTE: Because the assembly is used elsewhere, do not save changes.
'--------------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExtension As SldWorks.ModelDocExtension
Dim swFeatureManager As SldWorks.FeatureManager
Dim swFeature As SldWorks.Feature
Dim swDerivedPatternFeatureData As SldWorks.DerivedPatternFeatureData
Dim fileName As String
Dim status As Boolean
Dim errors As Long
Dim warnings As Long
Dim patternObj As SldWorks.Feature</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2019\samples\tutorial\api\assem2.sldasm&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocASSEMBLY, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swModelDocExtension = swModel.<b>Extension</b>
    Set swFeatureManager = swModel.<b>FeatureManager</b>    </pre>
<pre>    swModel.<b>ClearSelection2</b> True    </pre>
<pre>    ' Create derived pattern feature
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;Part2^assem2-7@assem2&quot;, &quot;COMPONENT&quot;, 0, 0, 0, False, 1, Nothing, 0)
    status = swModelDocExtension.<b>SelectByID2</b>(&quot;LPattern1@Part3^assem2-1@assem2&quot;, &quot;BODYFEATURE&quot;, 0, 0, 0, True, 2, Nothing, 0)
    </pre>
<blockquote>
	<p class="APICODE">Set swDerivedPatternFeatureData = swFeatureManager.<b>CreateDefinition</b>(swFmDerivedLPattern)<br>
	Set swFeature = swFeatureManager.<b>CreateFeature</b>(swDerivedPatternFeatureData)</p>
</blockquote>
<pre>
     ' Get derived pattern feature name and type
     If Not swFeature Is Nothing Then
       Debug.Print &quot;Derived pattern feature name = &quot; &amp; swFeature.<b>Name</b>
       Debug.Print &quot;  Type of feature: &quot; &amp; swFeature.<b>GetTypeName2</b>
       Set swDerivedPatternFeatureData = swFeature.<b>GetDefinition</b>()
       If Not swDerivedPatternFeatureData Is Nothing Then
          swDerivedPatternFeatureData.<b>AccessSelections</b> swModel, Nothing
          ' Get type of pattern feature used for derived pattern feature
          Set patternObj = swDerivedPatternFeatureData.<b>PatternFeature</b>
          Debug.Print &quot;    Type of pattern feature used for derived pattern feature: &quot; &amp; patternObj.<b>GetTypeName2
</b>          ' Get pattern feature name
          Debug.Print &quot;    Name of pattern feature used for the derived pattern feature: &quot; &amp; patternObj.<b>Name</b>
          Dim swLinearPatternFeatureData As SldWorks.LinearPatternFeatureData
          ' Get the pattern feature used for the derived pattern feature
          Set swLinearPatternFeatureData = patternObj.<b>GetDefinition</b>
          Dim nbrPatternInstances As Long
          nbrPatternInstances = (swLinearPatternFeatureData.<b>D1TotalInstances</b> * swLinearPatternFeatureData.<b>D2TotalInstances</b>)
          Debug.Print &quot;    Total number of pattern instances in pattern feature used for the derived pattern feature: &quot; &amp; nbrPatternInstances
          ' Get position of the pattern instance used as the seed feature in the derived pattern feature
          Debug.Print &quot;      Position of the pattern instance used as the seed feature in the derived pattern feature: &quot; &amp; swDerivedPatternFeatureData.<b>SeedPosition</b>
          ' Change position of the pattern instance to use as the seed feature in the derived pattern feature
          swDerivedPatternFeatureData.<b>SeedPosition</b> = nbrPatternInstances - 1
          Debug.Print &quot;      Modified position of the pattern instance to use as the seed feature in the derived pattern feature: &quot; &amp; swDerivedPatternFeatureData.<b>SeedPosition
</b>          swFeature.<b>ModifyDefinition</b> swDerivedPatternFeatureData, swModel, Nothing
       End If
    End If    </pre>
<pre>    swModel.<b>ClearSelection2</b> True    </pre>
<pre>End Sub</pre>

<p>&nbsp;</p>

</body>

</html>
