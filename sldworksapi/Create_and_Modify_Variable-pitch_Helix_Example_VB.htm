<html>

<head>
<title>Create and Modify Variable-pitch Helix Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,helix">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Create and Modify Variable-pitch Helix Example (VBA)</h1>
<p>This example shows how to create and modify a variable-pitch helix.</p>

<pre>'--------------------------------------------
' Preconditions:
' 1. Specified part template exits.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens a new part document.
' 2. Selects <b>Front Plane</b>, creates a circle, and
'    and uses the circle to create a variable-pitch
'    helix feature named <b>Helix/Spiral1</b>.
' 3. Gets whether the <b>Helix/Spiral1</b> feature is a
'    variable-pitch helix. If so:
'    a. Prints to the Immediate window the number
'       of regions.
'    b. Prints to the Immediate window
'       each region's diameter, pitch, height,
'       and revolution.
'    c. If region 2 of the variable-pitch helix
'       is defined by height and revolution:
'       1. Modifies region's diameter, height, and
'          revolution values.
'       2. Prints to the Immediate window the status
'          of modifications made in previous step.
'    d. Deletes the last region in the <b>Helix/Spiral1</b> feature and
'       prints the status of the deletion to the Immediate window.
'    e. Adds a new last region to the <b>Helix/Spiral1</b> feature and
'       prints the status of the addition to the Immediate window.
'    - or -
'    Prints to the Immediate window that the Helix/Spiral1
'    feature is not a variable-pitch helix.
' 4. Examine the Immediate window.
'--------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swPart As SldWorks.PartDoc
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swSketchMgr As SldWorks.SketchManager
Dim swFeatureMgr As SldWorks.FeatureManager
Dim swFeat As SldWorks.Feature
Dim swHelixFeatData As SldWorks.HelixFeatureData
Dim status As Boolean
Dim i As Long
Dim helixType As Long
Dim helixStatus As Long
Dim helixRegionArray(0) As Long</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks    </pre>
<pre>    Set swPart = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SOLIDWORKS\SOLIDWORKS 2015\templates\Part.prtdot&quot;, 0, 0, 0)
    Set swModel = swPart
    Set swModelDocExt = swModel.<b>Extension</b>    </pre>
<pre>    'Select plane on which to create circle
    'for variable-pitch helix
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Front Plane&quot;, &quot;PLANE&quot;, -5.71253507530972E-02, 5.36428819342089E-02, 3.49118658744337E-03, False, 0, Nothing, 0)
    Set swSketchMgr = swModel.<b>SketchManager</b>
    swSketchMgr.<b>InsertSketch</b> (True)
    'Create circle for variable-pitch helix
    swSketchMgr.<b>CreateCircle</b> 0.007581, 0.053509, 0#, 0.013533, 0.016475, 0#
    'Create a variable-pitch helix using the just-sketched circle
    Set swFeatureMgr = swModel.<b>FeatureManager</b>
    status = swFeatureMgr.<b>InsertVariablePitchHelix</b>(False, True, swHelixDefinedBy_e<b>.</b>swHelixDefinedByHeightAndRevolution, 4.712388980385)
    status = swFeatureMgr.<b>AddVariablePitchHelixFirstPitchAndDiameter</b>(0.053, 0.05382625271268)
    status = swFeatureMgr.<b>AddVariablePitchHelixSegment</b>(0.0265, 0.05382625271268, 0.5)
    status = swFeatureMgr.<b>AddVariablePitchHelixSegment</b>(0.03975, 0.05382625271268, 0.75)
    status = swFeatureMgr.<b>AddVariablePitchHelixSegment</b>(0.046375, 0.05382625271268, 0.875)
    status = swFeatureMgr.<b>AddVariablePitchHelixSegment</b>(0.053, 0.05382625271268, 1)
    Set swFeat = swFeatureMgr.<b>EndVariablePitchHelix</b>()    </pre>
<pre>    'Get variable-pitch helix
    Set swHelixFeatData = swFeat.<b>GetDefinition</b>
    If swHelixFeatData.<b>VariablePitch</b> Then
        Debug.Print (&quot;  Number of regions: &quot; &amp; swHelixFeatData.<b>PitchCount</b>)
            For i = 1 To swHelixFeatData.<b>PitchCount</b>
            Debug.Print (&quot;   Region &quot; &amp; i &amp; &quot;:&quot;)
            Debug.Print (&quot;      Diameter: &quot; &amp; swHelixFeatData.<b>GetRegionParameterAtIndex</b>(i, swVariablePitchHelixRegionParameter_e.swVariablePitchHelixRegionParameter_Diameter))
            Debug.Print (&quot;      Pitch: &quot; &amp; swHelixFeatData.<b>GetRegionParameterAtIndex</b>(i, swVariablePitchHelixRegionParameter_e.swVariablePitchHelixRegionParameter_Pitch))
            Debug.Print (&quot;      Height: &quot; &amp; swHelixFeatData.<b>GetRegionParameterAtIndex</b>(i, swVariablePitchHelixRegionParameter_e.swVariablePitchHelixRegionParameter_Height))
            Debug.Print (&quot;      Revolutions: &quot; &amp; swHelixFeatData.<b>GetRegionParameterAtIndex</b>(i, swVariablePitchHelixRegionParameter_e.swVariablePitchHelixRegionParameter_Revolution))
        Next i            </pre>
<pre>        'Modify region 2 of variable-pitch helix
        'defined by height and revolution
        helixType = swHelixFeatData.<b>DefinedBy
</b>
        Select Case helixType        </pre>
<pre>            Case swHelixDefinedBy_e<b>.</b>swHelixDefinedByHeightAndRevolution
                If i &gt;= 2 Then
                    'Cannot change pitch
                    'Can change diameter, height, and revolution
                    'Revolution must be smaller than previous region's
                    'revolution and less than next region's revolution
                    Debug.Print (&quot;&quot;)
                    Debug.Print (&quot;Helix defined by height and revolution:&quot;)
                    Debug.Print (&quot;   Region modified: 2&quot;)
                    helixStatus = swHelixFeatData.<b>SetRegionParameterAtIndex</b>(2, swVariablePitchHelixRegionParameter_e.swVariablePitchHelixRegionParameter_Diameter, 0.052)
                    Debug.Print (&quot;      Diameter modified (0 = success): &quot; &amp; helixStatus)
                    helixStatus = swHelixFeatData.<b>SetRegionParameterAtIndex</b>(2, swVariablePitchHelixRegionParameter_e.swVariablePitchHelixRegionParameter_Height, 0.025)
                    Debug.Print (&quot;      Height modified (0 = success): &quot; &amp; helixStatus)
                    helixStatus = swHelixFeatData.<b>SetRegionParameterAtIndex</b>(2, swVariablePitchHelixRegionParameter_e.swVariablePitchHelixRegionParameter_Revolution, 0.45)
                    Debug.Print (&quot;      Revolution modified (0 = success): &quot; &amp; helixStatus)
                Else
                    Debug.Print &quot;Less than three regions in Helix/Spiral1 feature.)&quot;
                End If</pre>
<pre>            Case swHelixDefinedBy_e.swHelixDefinedByHeightAndPitch
            'Cannot change revolution
            'TODO: Add code for variable-pitch helix defined by height and pitch
            </pre>
<pre>            Case swHelixDefinedBy_e.swHelixDefinedByPitchAndRevolution
            'Cannot change height
            'TODO: Add code for variable-pitch helix defined by pitch and revolution            </pre>
<pre>        End Select              </pre>
<pre>        'Delete last region in the Helix/Spiral1 feature
        helixRegionArray(0) = (i - 1)
        Debug.Print (&quot;&quot;)
        status = swHelixFeatData.<b>DeleteRecord</b>(helixRegionArray)
        Debug.Print &quot;Last region in Helix/Spiral1 deleted; i.e., region &quot; &amp; i - 1 &amp; &quot; was deleted: &quot; &amp; status   </pre>
<pre>        'Add new region to end of Region parameters table
        Dim record(3) As Double
        'Height
        record(0) = 0.055
        'Number of revolutions
        record(1) = 1
        '0 indicates that this value cannot be specified
        'for this type of variable-pitch helix (Height and Revolution)
        'Instead, SOLIDWORKS calculates it
        record(2) = 0
        'Diameter
        record(3) = 0.05382625271268
        status = swHelixFeatData.<b>AddLastRecord</b>(record)
        Debug.Print &quot;New region 5 added as last record to Helix/Spiral1: &quot; &amp; status</pre>
<pre>        status = swFeat.<b>ModifyDefinition</b>(swHelixFeatData, swModel, Nothing)        </pre>
<pre>    Else    </pre>
<pre>        Debug.Print &quot;Helix is not variable pitch.&quot;        </pre>
<pre>    End If</pre>
<pre>End Sub</pre>

</body>

</html>
