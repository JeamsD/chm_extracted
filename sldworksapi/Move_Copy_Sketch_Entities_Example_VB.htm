<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Move Copy Sketch Entities Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=MS-HKWD content="VBA examples,move/copy">
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=1786>
<meta name=layout-width content=777>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./ApiHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Move Copy Sketch Entities Example (VBA)</h1>

<p>This example shows how to move, copy, and move and copy sketch entities.</p>
<pre>'----------------------------------------------------------------
' Preconditions: Verify that the specified part template exists.
'
' Postconditions:
' 1. Step through the macro by pressing F8.
'     1.  Opens a new part document.
'     2.  Opens a sketch and sketches a line and a circle.
'     3.  Examine the sketch.
'     4.  Selects the line and circle.
'     5.  Moves the line and circle.
'     6.  Examine the sketch.
'     7.  Moves and copies the line and circle once.
'     8.  Examine the sketch.
'     9.  Moves and copies the line and circle twice.
'    10. Examine the sketch.
' 2. Close the part document without saving it.
'----------------------------------------------------------------
Option Explicit</pre>
<pre> </pre>
<pre>Dim swApp              As SldWorks.SldWorks
Dim swModel            As SldWorks.ModelDoc2
Dim swModelDocExt      As SldWorks.ModelDocExtension
Dim swPart             As SldWorks.PartDoc
Dim swFeature          As SldWorks.Feature
Dim swSketchMgr        As SldWorks.SketchManager
Dim swSketchSegment    As SldWorks.SketchSegment
Dim lIdx               As Long
Dim bCopy              As Boolean
Dim lNumCopies         As Long
Dim aBasePoint(2)      As Double
Dim aMoveVector(2)     As Double
Dim errors             As Long
Dim status             As Boolean</pre>
<pre>Sub main()</pre>
<pre>    Set swApp = Application.SldWorks</pre>
<pre>    ' Open a new part document and sketch a line and a circle
    Set swModel = swApp.<b>NewDocument</b>(&quot;C:\ProgramData\SOLIDWORKS\SOLIDWORKS 2016\templates\Part.prtdot&quot;, 0, 0, 0)
    swApp.<b>ActivateDoc3</b> &quot;Part1&quot;, True, False, errors
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swPart = swModel
    Set swModelDocExt = swModel.<b>Extension</b>
    Set swSketchMgr = swModel.<b>SketchManager</b>
    swSketchMgr.<b>InsertSketch</b> True
    Set swSketchSegment = swSketchMgr.<b>CreateLine</b>(-0.096389, 0.032667, 0#, -0.062943, 0.019437, 0#)
    Set swSketchSegment = swSketchMgr.<b>CreateCircle</b>(-0.084504, 0.013823, 0#, -0.087932, 0.006083, 0#)</pre>
<pre>    Set swFeature = swPart.<b>FeatureByName</b>(&quot;Sketch1&quot;)
    status = swFeature.<b>Select2</b>(False, 0)
    swModel.EditSketch</pre>
<pre>    aBasePoint(0) = 0#
    aBasePoint(1) = 0#
    aBasePoint(2) = 0#</pre>
<pre>    aMoveVector(0) = 0.1
    aMoveVector(1) = 0#
    aMoveVector(2) = 0#
    
</pre>
<pre>    For lIdx = 0 To 2
        swModel.<b>ClearSelection2</b> True
        status = swModelDocExt.<b>SelectByID2</b>(&quot;Line1&quot;, &quot;SKETCHSEGMENT&quot;, -7.52087432116777E-02, 3.68667656031986E-02, 1.46398923143701E-02, True, 0, Nothing, 0)
        status = swModelDocExt.<b>SelectByID2</b>(&quot;Arc1&quot;, &quot;SKETCHSEGMENT&quot;, -8.02420935887737E-02, 3.33695230163339E-02, 0.019671897706856, True, 0, Nothing, 0)</pre>
<pre>        Select Case (lIdx)
            Case 0
            ' Move
                bCopy = False
                lNumCopies = 0
            Case 1
            ' Move and copy once
                bCopy = True
                lNumCopies = 1
            Case 2
            ' Move and copy twice
                bCopy = True
                lNumCopies = 2
        End Select</pre>
<pre>        If (Not bCopy) Then
            lNumCopies = 0
        End If</pre>
<pre>        swModelDocExt.<b>MoveOrCopy</b> bCopy, lNumCopies, True, aBasePoint(0), aBasePoint(1), 0#, aBasePoint(0) + aMoveVector(0), aBasePoint(1) + aMoveVector(1), aBasePoint(2) + aMoveVector(2)
        swModel.<b>ClearSelection2</b> True</pre>
<pre>        ' Undo so that you can do it again, but differently
        swModel.<b>EditUndo2</b> 1</pre>
<pre>    Next lIdx</pre>
<pre>    swSketchMgr.<b>InsertSketch</b> True</pre>
<pre>End Sub</pre>

</body>

</html>
