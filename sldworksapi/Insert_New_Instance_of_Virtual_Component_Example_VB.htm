<html>

<head>
<title>Insert New Instance of Virtual Component (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,virtual component">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Insert New Instance of Virtual Component (VBA)</h1>
<p>This example shows how to:</p>
<ul>
	<li><font face="Verdana, sans-serif" style="font-size: 8pt">create an 
	assembly document. </font></li>
	<li><font face="Verdana, sans-serif" style="font-size: 8pt">insert a new 
	part as a virtual component in the assembly document.</font></li>
	<li><font face="Verdana, sans-serif" style="font-size: 8pt">insert a new 
	instance of the virtual component in the assembly document.</font></li>
</ul>
<pre>'-----------------------------------------------------
' Preconditions: None
'
' Postconditions:
' 1. An assembly document is created.
' 2. A virtual component is inserted in the assembly document.
' 3. A new instance of the virtual component is inserted
'    in the assembly document.
' 4. Examine the FeatureManager design tree to
'    verify steps 2 and 3.
' 5. Close the assembly document without saving the modified
'    documents.
'-----------------------------------------------------</pre>
<pre>Option Explicit
</pre>
<pre>Sub main()
    Dim swApp As SldWorks.SldWorks
    Set swApp = Application.SldWorks
    </pre>
<pre>    Dim asmTemplate As String
    asmTemplate = swApp.<b>GetUserPreferenceStringValue</b>(swUserPreferenceStringValue_e.swDefaultTemplateAssembly)
    </pre>
<pre>    Dim swModel As SldWorks.ModelDoc2
    Set swModel = swApp.<b>NewDocument</b>(asmTemplate, 0, 0, 0)
    </pre>
<pre>    Dim swSelMgr As SldWorks.SelectionMgr
    Set swSelMgr = swModel.<b>SelectionManager</b>
    </pre>
<pre>    If swModel.Extension.<b>SelectByID2</b>(&quot;Front Plane&quot;, &quot;PLANE&quot;, 0, 0, 0, False, 0, Nothing, 0) = False Then
        Debug.Print &quot;Failed to select Front plane; check feature name.&quot;
        Exit Sub
    End If
    </pre>
<pre>    Dim swPlaneFeature As SldWorks.Feature
    Set swPlaneFeature = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Dim swPlane As SldWorks.refPlane
    Set swPlane = swPlaneFeature.<b>GetSpecificFeature2</b>
    </pre>
<pre>    Dim swAssem As SldWorks.AssemblyDoc
    Set swAssem = swModel
    </pre>
<pre>    Dim lResult As Long
    Dim swVirtComp As SldWorks.Component2
    lResult = swAssem.<b>InsertNewVirtualPart</b>(swPlane, swVirtComp)
    </pre>
<pre>    If lResult = swInsertNewPartError_NoError Then
        Dim swSecondComp As SldWorks.Component2
        Set swSecondComp = swAssem.<b>AddComponent5</b>(swVirtComp.GetPathName, swAddComponentConfigOptions_CurrentSelectedConfig, &quot;&quot;, False, &quot;&quot;, 0.1, 0, 0)
    End If
    </pre>
<pre>End Sub</pre>

</body>

</html>
