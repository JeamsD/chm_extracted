<html>

<head>
<title>Get Number of Instances Skipped in Driving Feature Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,derived patterns">
<meta name=MS-HKWD content="VBA examples,patterns">
<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Get Number of Instances Skipped in Driving Feature Example (VBA)</h1>
<p>This example shows how to get the number of instances skipped in the driving feature 
of a derived pattern feature.</p>

<pre>'--------------------------------------------------------------------------------
' Preconditions:
' 1. Open <i>public_documents</i><b>\samples\tutorial\api\PatternDrivenComponentPattern.sldasm</b>.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Adds instances to skip in the local linear pattern feature.
' 2. Inserts a derived pattern feature.
' 3. Gets the number of instances skipped in the driving feature for the
'    derived pattern feature.
' 4. Gets whether visual properties are propagated in the derived pattern
'    feature.
' 5. Examine the FeatureManager design tree, graphics area, and
'    Immediate window.
'
' NOTE: Because the assembly is used elsewhere, do not save changes.
'--------------------------------------------------------------------------------
Option Explicit</pre>
<pre>    Dim SwApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swAssemblyDoc as SldWorks.AssemblyDoc
    Dim swModelDocExt As SldWorks.ModelDocExtension
    Dim swSelectionMgr As SldWorks.SelectionMgr
    Dim swFeature As SldWorks.Feature
    Dim swLocalLinearPatternData As SldWorks.LocalLinearPatternFeatureData
    Dim swDerivedPatternFeatureData As SldWorks.DerivedPatternFeatureData
    Dim list(1) As Long
    Dim instancesSkipData As Variant
    Dim instancesSkipData_Derived As Variant
    Dim instancesToSkipCount As Long
    Dim status As Boolean
    Dim i As Long    </pre>
<pre>Sub main()</pre>
<pre>    Set SwApp = Application.SldWorks
    Set swModel = SwApp.<b>ActiveDoc</b>
    Set swModelDocExt = swModel.<b>Extension</b>    </pre>
<pre>    'Add instances to skip to local linear pattern feature, LocalLPattern1
    status = swModelDocExt.<b>SelectByID2</b>(&quot;LocalLPattern1&quot;, &quot;COMPPATTERN&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swSelectionMgr = swModel.<b>SelectionManager</b>
    Set swFeature = swSelectionMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swLocalLinearPatternData = swFeature.<b>GetDefinition</b>
    status = swLocalLinearPatternData.<b>AccessSelections</b>(swModel, Nothing)
    list(0) = 11
    list(1) = 14
    instancesSkipData = list
    swLocalLinearPatternData.<b>SkippedItemArray</b> = instancesSkipData
    status = swFeature.<b>ModifyDefinition</b>(swLocalLinearPatternData, swModel, Nothing)</pre>
<pre>    'Insert derived pattern feature, LocalCompPattern1
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Part2^patterndrivencomponentpattern-1@patterndrivencomponentpattern&quot;, &quot;COMPONENT&quot;, 0, 0, 0, False, 1, Nothing, 0)
    status = swModelDocExt.<b>SelectByID2</b>(&quot;LocalLPattern1&quot;, &quot;COMPPATTERN&quot;, 0, 0, 0, True, 6, Nothing, 0)
    Set swAssemblyDoc = swModel
    status = swAssemblyDoc.<b>InsertDerivedPattern</b>()    </pre>
<pre>    'Get number of instances skipped in the driving feature, LocalLPattern1,
    'for the derived pattern feature, LocalCompPattern1
    status = swModelDocExt.<b>SelectByID2</b>(&quot;LocalCompPattern1&quot;, &quot;COMPPATTERN&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swFeature = swSelectionMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swDerivedPatternFeatureData = swFeature.<b>GetDefinition</b>
    status = swDerivedPatternFeatureData.<b>AccessSelections</b>(swModel, Nothing)
    instancesSkipData_Derived = swDerivedPatternFeatureData.<b>DrivingFeatureSkippedItemArray</b>
    instancesToSkipCount = UBound(instancesSkipData_Derived)
    Debug.Print &quot;Number of instances skipped in the driving feature of the derived pattern feature: &quot; &amp; instancesToSkipCount + 1
    For i = 0 To instancesToSkipCount
        Debug.Print &quot;  Instance skipped: &quot; &amp; instancesSkipData_Derived(i)
    Next i
    swDerivedPatternFeatureData.<b>ReleaseSelectionAccess</b></pre>
<pre>    'Get whether visual properties are propagated in the derived pattern feature
    Debug.Print &quot;Visual properties propagated in the derived pattern feature? &quot; &amp; swDerivedPatternFeatureData.<b>PropagateVisualProperty</b></pre>
<pre>End Sub</pre>

</body>

</html>
