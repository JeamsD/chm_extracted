<html>

<head>
<title>Create Cut-sweep Feature Using Circular Profile Example (VBA)</title>
<meta http-equiv="Content-Language" content="en-us">
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<link rel="stylesheet" type="text/css" href="ApiHelp.css">
</head>
<meta name=MS-HKWD content="VBA examples,sweeps">

<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>
<h1>Create Cut-sweep Feature Using Circular Profile Example (VBA)</h1>
<p>This example shows how to create a cut-sweep feature using a circular profile.</p>

<pre>'-----------------------------------------------
' Preconditions:
' 1. Verify that the part exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the part.
' 2. Selects an edge on the part.
' 3. Creates a cut-sweep feature using a circular
'    profile.
' 4. Accesses the cut-sweep feature.
' 5. Changes the diameter of the circular profile.
' 6. Examine the Immediate window, FeatureManager
'    design tree, and graphics area.
'
' NOTE: Because the part is used elsewhere, do not
' save changes.
'------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim swFeature As SldWorks.Feature
Dim swFeatureManager As SldWorks.FeatureManager
Dim swSweepFeatureData As SldWorks.SweepFeatureData
Dim status As Boolean
Dim errors As Long
Dim warnings As Long

Sub main()</pre>
<pre>    Set swApp = Application.SldWorks
    Set swModel = swApp.<b>OpenDoc6</b>(&quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\block20.sldprt&quot;, 1, 0, &quot;&quot;, errors, warnings)
    Set swModelDocExt = swModel.<b>Extension</b>    </pre>
<pre>    'Select the edge for the cut-sweep feature 
    status = swModelDocExt.<b>SelectByID2</b>(&quot;&quot;, &quot;EDGE&quot;, -3.72983839416747E-02, 3.93904284381961E-02, 4.95042874504179E-02, True, 4, Nothing, 0)
    Set swFeatureManager = swModel.<b>FeatureManager</b>    </pre>
<pre>    'Create the cut-sweep feature using a circular profile
    Set swFeature = swFeatureManager.<b>InsertCutSwept5</b>(False, True, 0, False, False, 0, 0, False, 0, 0, 0, 0, True, True, 0, True, True, True, False, True, 0.0254, swSweepDirection_e.swSweepDirection1)    </pre>
<pre>    'Roll back the cut-sweep feature to access and change the diameter of circular profile
    Set swSweepFeatureData = swFeature.<b>GetDefinition</b>
    status = swSweepFeatureData.<b>AccessSelections</b>(swModel, Nothing)
    Debug.Print &quot;Using a circular profile? &quot; &amp; swSweepFeatureData.<b>CircularProfile</b>
    Debug.Print &quot;Original size of circular profile = &quot; &amp; swSweepFeatureData.<b>CircularProfileDiameter</b>
    swSweepFeatureData.<b>CircularProfileDiameter</b> = 0.003
    Debug.Print &quot;Modified size of circular profile = &quot; &amp; swSweepFeatureData.<b>CircularProfileDiameter</b>
    Debug.Print &quot;Direction of cut-sweep = &quot; &amp; swSweepFeatureData.<b>Direction</b>    </pre>
<pre>    'Roll forward the cut-sweep feature
    swFeature.ModifyDefinition <b>swSweepFeatureData</b>, swModel, Nothing    </pre>
<pre>End Sub</pre>

</body>

</html>
