<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Insert Autoballoons Example (VBA) (AutoBalloon2)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,autoballoons">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2021>
<meta name=layout-width content=786>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Insert Autoballoons Example (VBA) </h1>

<p>This example shows how to insert autoballoons in a drawing document 
 using the now obsolete method IDrawingDoc::AutoBalloon2.</p>
<pre>'------------------------------------------------------------------
' Preconditions:
' 1. Verify that the specified drawing document to open exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the specified drawing document.
' 2. Selects a drawing view.
' 3. Inserts autoballoons for each resolved component in
'    the selected drawing view.
' 4. Examine the drawing and Immediate window.
'
' NOTE: Because the drawing is used elsewhere, do not save changes.
'------------------------------------------------------------------</pre>
<pre>Sub main()</pre>
<pre>    Const swDetailingBalloonLayout_Default As Long = -1
    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swModelDocExt As SldWorks.ModelDocExtension
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swDraw As SldWorks.DrawingDoc
    Dim swView As SldWorks.View
    Dim vNoteArr As Variant
    Dim vNote As Variant
    Dim swNote As SldWorks.Note
    Dim swAnn As SldWorks.Annotation
    Dim vAttachPos As Variant
    Dim vAnnPos As Variant
    Dim bRet  As Boolean
    Dim fileName As String
    Dim errors As Long
    Dim warnings As Long</pre>
<pre>    Set swApp = Application.SldWorks</pre>
<pre>    ' Open drawing
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\advdrawings\foodprocessor.slddrw&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocDRAWING, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)
    Set swModelDocExt = swModel.<b>Extension</b>
    bRet = swModelDocExt.<b>SelectByID2</b>(&quot;Drawing View1&quot;, &quot;DRAWINGVIEW&quot;, 0, 0, 0, False, 0, Nothing, 0)
    </pre>
<pre>    Set swDraw = swModel
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swView = swSelMgr.<b>GetSelectedObject6</b>(1, -1)</pre>
<pre>    bRet = swDraw.<b>ActivateView</b>(swView.Name): Debug.Assert bRet</pre>
<pre>    Debug.Print &quot;File = &quot; &amp; swModel.<b>GetPathName</b></pre>
<pre>    ' Get autoballoon settings
    Debug.Print &quot;  DetailingBalloonsDisplayWithBentLeader = &quot; &amp; swModel.<b>GetUserPreferenceToggle</b>(swDetailingBalloonsDisplayWithBentLeader)
    Debug.Print &quot;  DetailingBOMBalloonStyle = &quot; &amp; swModel.<b>GetUserPreferenceIntegerValue</b>(swDetailingBOMBalloonStyle)
    Debug.Print &quot;  DetailingBOMBalloonFit = &quot; &amp; swModel.<b>GetUserPreferenceIntegerValue</b>(swDetailingBOMBalloonFit)
    Debug.Print &quot;  DetailingAutoBalloonLayout = &quot; &amp; swModel.<b>GetUserPreferenceIntegerValue</b>(swDetailingAutoBalloonLayout)
    Debug.Print &quot;  DetailingBalloonBentLeaderLength = &quot; &amp; swModel.<b>GetUserPreferenceDoubleValue</b>(swDetailingBalloonBentLeaderLength) * 1000# &amp; &quot; mm&quot;</pre>
<pre>    Debug.Print &quot;&quot;
    Debug.Print &quot;  &quot; &amp; swView.<b>Name</b></pre>
<pre>    vNoteArr = swDraw.<b>AutoBalloon2</b>(swDetailingBalloonLayout_Default, False)
    
</pre>
<pre>    ' Returns an empty array if:
    ' *  Balloons already exist in any drawing view on any on sheet in
    '    the drawing document.
    ' *  Drawing document is lightweight.
    ' Returns a note for each resolved component in the selected drawing view.
    If IsEmpty(vNoteArr) Then
        Debug.Print &quot;    No balloons added.&quot;
        Exit Sub
    End If</pre>
<pre>    For Each vNote In vNoteArr
        Set swNote = vNote
        Set swAnn = swNote.<b>GetAnnotation</b>
        vAttachPos = swNote.<b>GetAttachPos</b>
        vAnnPos = swAnn.<b>GetPosition</b></pre>
<pre>        Debug.Print &quot;    Autoballon text = &quot; &amp; swNote.<b>GetText</b>
        If swAnn.<b>Layer</b> = &quot;&quot; Then
            Debug.Print &quot;      No layers defined.&quot;
        Else
            Debug.Print &quot;      Layer = &quot; &amp; swAnn.<b>Layer</b>
        End If
        Debug.Print &quot;      Note attachment point = (&quot; &amp; vAttachPos(0) * 1000# &amp; &quot;, &quot; &amp; vAttachPos(1) * 1000# &amp; &quot;, &quot; &amp; vAttachPos(2) * 1000# &amp; &quot;) mm&quot;
        Debug.Print &quot;      Origin of annotation = (&quot; &amp; vAnnPos(0) * 1000# &amp; &quot;, &quot; &amp; vAnnPos(1) * 1000# &amp; &quot;, &quot; &amp; vAnnPos(2) * 1000# &amp; &quot;) mm&quot;
    Next</pre>
<pre>End Sub</pre>

</body>

</html>
