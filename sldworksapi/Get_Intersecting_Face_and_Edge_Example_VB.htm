<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Get Intersecting Face and Edge Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2010>
<meta name=layout-width content=628>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">

 <META NAME="MS-HKWD" CONTENT="VBA examples,faces">
 <META NAME="MS-HKWD" CONTENT="VBA examples,edges">
  <META NAME="MS-HKWD" CONTENT="VBA examples,curves">
   <META NAME="MS-HKWD" CONTENT="VBA examples,surfaces">
</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API Help</span></h1>

<h1>Get Intersecting Face and Edge Example (VBA)</h1>

<p>This example shows how to get the intersection of a face and an edge.</p>
<p><b>NOTE:</b> A face and an edge intersect in a series of points. This example 
shows how to use some of the geometry- and topology-related methods to get the 
intersection points.</p>
<pre>'-------------------------------------------------------
' Preconditions:
' 1. Open a part or a fully resolved assembly.
' 2. Select an intersecting face and edge, in that
'    order, in the graphics area.
'
' Postconditions:
' 1. Inserts a 3D sketch with sketch points located
'    at the intersection of the face and edge.
' 2. Examine the graphics area and FeatureManager design
'    tree.
'-------------------------------------------------------
Option Explicit</pre>
<pre>Sub CreatePoints(swApp As SldWorks.SldWorks, swModel As SldWorks.ModelDoc2, vPtArray As Variant)
    Dim swSketchPt As SldWorks.SketchPoint
    Dim bRet As Boolean
    Dim i As Long</pre>
<pre>    swModel.<b>SetAddToDB</b> True
    swModel.<b>Insert3DSketch2</b> False</pre>
<pre>    For i = 0 To UBound(vPtArray) Step 3
        Set swSketchPt = swModel.<b>CreatePoint2</b>(vPtArray(i + 0), vPtArray(i + 1), vPtArray(i + 2))
    Next i    </pre>
<pre>    swModel.<b>SetAddToDB</b> False
    swModel.<b>Insert3DSketch2</b> True</pre>
<pre>    bRet = swModel.<b>EditRebuild3</b></pre>
<pre>End Sub</pre>
<pre>Sub main()</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swFace As SldWorks.Face2
    Dim swSurf As SldWorks.Surface
    Dim swEdge As SldWorks.Edge
    Dim swCurve As SldWorks.Curve
    Dim vPointArray As Variant
    Dim vTArray As Variant
    Dim vUVArray As Variant
    Dim vCurveParam As Variant
    Dim vCurveBounds As Variant
    Dim nCurveBounds(0 To 5) As Double
    Dim i As Long
    Dim bRet As Boolean</pre>
<pre>    Set swApp = CreateObject(&quot;SldWorks.Application&quot;)
    Set swModel = swApp.<b>ActiveDoc</b>
    Set swSelMgr = swModel.<b>SelectionManager</b>
    Set swFace = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swEdge = swSelMgr.<b>GetSelectedObject6</b>(2, -1)
    Set swSurf = swFace.<b>GetSurface</b>
    Set swCurve = swEdge.<b>GetCurve</b></pre>
<pre>    vCurveParam = swEdge.<b>GetCurveParams2</b></pre>
<pre>    For i = 0 To 5
        nCurveBounds(i) = vCurveParam(i)
    Next i
    vCurveBounds = nCurveBounds
    </pre>
<pre>    bRet = swSurf.<b>IntersectCurve</b>(swCurve, (vCurveBounds), vPointArray, vTArray, vUVArray)

    CreatePoints swApp, swModel, vPointArray</pre>
<pre>End Sub

</pre>

</body>

</html>
