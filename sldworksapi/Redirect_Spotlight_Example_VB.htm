<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Redirect Spotlight Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<meta name=MS-HKWD content="VBA examples,lighting">
<meta name=MS-HKWD content="VBA examples,spotlights">
<meta name=MS-HKWD content="Spot lights">
<meta name=MS-HKWD content=Lighting>
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=2096>
<meta name=layout-width content=750>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./APIHelp.css">



</head>

<!--(Body)==========================================================-->


<body>

<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS API 
 Help</span></h1>

<h1>Redirect Spotlight Example (VBA)</h1>

<p>This examples shows how select a spotlight and two sketch points. <!--kadov_tag{{<spaces>}}-->&nbsp;<!--kadov_tag{{</spaces>}}-->The 
 spotlight is then redirected to point along a vector that is defined 
 by the two points. <!--kadov_tag{{<spaces>}}-->&nbsp;<!--kadov_tag{{</spaces>}}--></p>

<pre>'-------------------------------------------------------------------------
' Preconditions:
' 1. Verify that the specified assembly exists.
' 2. Open the Immediate window.
'
' Postconditions:
' 1. Opens the assembly.
' 2. Adds a spotlight.
' 3. Inserts a 3D sketch.
' 4. Selects the spotlight.
' 5. Selects a new root point and direction point for the spotlight.
' 6. Gets the current position and direction of the spotlight.
' 7. Modifies the position and direction of the spotlight.
' 8. Examine the Immediate window.
'
' NOTE: Because the assembly is used elsewhere, do not save changes.
'---------------------------------------------------------------------------
Option Explicit</pre>
<pre>Sub main()</pre>
<pre>    Dim swApp As SldWorks.SldWorks
    Dim swModel As SldWorks.ModelDoc2
    Dim swModelDocExt As SldWorks.ModelDocExtension
    Dim swModelView As SldWorks.ModelView
    Dim swFeature As SldWorks.Feature
    Dim swSelMgr As SldWorks.SelectionMgr
    Dim swSketchManager As SldWorks.SketchManager
    Dim swSketch As SldWorks.Sketch
    Dim swMath As SldWorks.MathUtility
    Dim swLightFeature As SldWorks.Feature
    Dim swLight As SldWorks.Light
    Dim selRootSkPt As SldWorks.SketchPoint
    Dim selDirSkPt As SldWorks.SketchPoint
    Dim swSketchTrans As SldWorks.MathTransform
    Dim ptArr(2) As Double
    Dim dirArr(2) As Double
    Dim rootPoint As SldWorks.MathPoint
    Dim dirPoint As SldWorks.MathPoint
    Dim dirVector As SldWorks.MathVector
    Dim params As Variant
    Dim skSegment As Object
    Dim status As Boolean
    Dim errors As Long
    Dim warnings As Long
    Dim fileName As String
    Dim rect As Variant    </pre>
<pre>    Set swApp = Application.SldWorks    </pre>
<pre>    'Open assembly
    fileName = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\appearances\usb_flash_drive1.sldasm&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(fileName, swDocumentTypes_e.swDocASSEMBLY, swOpenDocOptions_e.swOpenDocOptions_Silent, &quot;&quot;, errors, warnings)    </pre>
<pre>    'Add spotlight
    status = swModel.<b>AddLightSource</b>(&quot;SW#5&quot;, 2, &quot;Spot1&quot;)
    status = swModel.<b>SetLightSourcePropertyValuesVB</b>(&quot;SW#5&quot;, 2, 0.5, 8454143, 1, -9.99999999999991E-02, 0.170000000000101, 1.10999999999999, 0.179999999999999, -9.00000000001008E-02, -1.02999999999999, 45, 0, 0, 0, 0.4, 0.4, 0, False)
    status = swModel.<b>LockLightToModel</b>(4, True)
    Set swModelView = swModel.<b>ActiveView</b>
    swModelView.<b>GraphicsRedraw</b> (rect)    </pre>
<pre>    'Insert 3D sketch
    Set swSketchManager = swModel.<b>SketchManager</b>
    swSketchManager.<b>Insert3DSketch</b> True
    Set skSegment = swSketchManager.<b>CreateLine</b>(-0.038076, 0.043671, -0#, -0.01322, 0.054563, -0#)
    Set swSketch = swModel.<b>GetActiveSketch2</b>()
    status = swSketch.<b>SetWorkingPlaneOrientation</b>(0, 0, 0, 0, 1, 0, 0, 0, 1, 1, 0, 0)
    Set skSegment = swSketchManager.<b>CreateLine</b>(-0.01322, 0.054563, -0#, -0.01322, 0.08124, 0.018547)
    Set swSketch = swModel.<b>GetActiveSketch2</b>()
    status = swSketch.<b>SetWorkingPlaneOrientation</b>(0, 0, 0, 0, 0, 1, 1, 0, 0, 0, 1, 0)
    Set skSegment = swSketchManager.<b>CreateLine</b>(-0.01322, 0.08124, 0.018547, 0.000568, 0.08124, 0.004759)
    Set swSketch = swSketchManager.<b>ActiveSketch</b>
    status = swSketch.<b>SetWorkingPlaneOrientation</b>(0, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1)
    swModel.<b>ClearSelection2</b> True
    swSketchManager.<b>InsertSketch</b> True    </pre>
<pre>    'Select the light
    Set swModelDocExt = swModel.<b>Extension</b>
    Set swSelMgr = swModel.<b>SelectionManager</b>
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Spot1&quot;, &quot;LIGHTS&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set swLightFeature = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swLight = swLightFeature.<b>GetSpecificFeature2</b>    </pre>
<pre>    'Select a root point
    status = swModel.Extension.<b>SelectByID2</b>(&quot;Point3@3DSketch1&quot;, &quot;EXTSKETCHPOINT&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set selRootSkPt = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swSketch = selRootSkPt.<b>GetSketch</b>    </pre>
<pre>    'Need the sketch to model transform
    Set swMath = swApp.<b>GetMathUtility</b>
    Set swSketchTrans = swSketch.<b>ModelToSketchTransform</b>.<b>Inverse</b>
    ptArr(0) = selRootSkPt.<b>X</b>
    ptArr(1) = selRootSkPt.<b>Y</b>
    ptArr(2) = selRootSkPt.<b>Z</b>
    Set rootPoint = swMath.<b>CreatePoint</b>((ptArr))    </pre>
<pre>    'Make sure that the point is in model space
    Set rootPoint = rootPoint.<b>MultiplyTransform</b>(swSketchTrans)    </pre>
<pre>    'Select a direction point
    status = swModelDocExt.<b>SelectByID2</b>(&quot;Point1@3DSketch1&quot;, &quot;EXTSKETCHPOINT&quot;, 0, 0, 0, False, 0, Nothing, 0)
    Set selDirSkPt = swSelMgr.<b>GetSelectedObject6</b>(1, -1)
    Set swSketch = selDirSkPt.<b>GetSketch</b>
    Set swSketchTrans = swSketch.<b>ModelToSketchTransform</b>.Inverse
    ptArr(0) = selDirSkPt.<b>X</b>
    ptArr(1) = selDirSkPt.<b>Y</b>
    ptArr(2) = selDirSkPt.<b>Z</b>
    Set dirPoint = swMath.<b>CreatePoint</b>((ptArr))
    Set dirPoint = dirPoint.<b>MultiplyTransform</b>(swSketchTrans)    </pre>
<pre>    'Determine the direction of the spotlight
    Set dirVector = dirPoint.<b>Subtract</b>(rootPoint)
    params = rootPoint.<b>ArrayData</b>
    ptArr(0) = params(0)
    ptArr(1) = params(1)
    ptArr(2) = params(2)
    params = dirVector.<b>ArrayData</b>
    dirArr(0) = params(0)
    dirArr(1) = params(1)
    dirArr(2) = params(2)    </pre>
<pre>    'Get the current parameters for the spotlight
    params = swModel.<b>LightSourcePropertyValues</b>(swLight.<b>GetID</b>())
    Dim nbrParams As Variant
    nbrParams = UBound(params)
    Dim i As Long
    Debug.Print (&quot;Current position(3) and direction(3)&quot;)
    For i = 5 To (nbrParams - 8)
        Debug.Print params(i)
    Next i   </pre>
<pre>    'Modify the root point and direction vector, then apply the changes
    params(5) = ptArr(0)
    params(6) = ptArr(1)
    params(7) = ptArr(2)
    params(8) = dirArr(0)
    params(9) = dirArr(1)
    params(10) = dirArr(2)
    swModel.<b>LightSourcePropertyValues</b>(swLight.<b>GetID</b>()) = params
    Debug.Print &quot;&quot;
    nbrParams = UBound(params)
    Debug.Print (&quot;Modified position(3) and direction(3)&quot;)
    For i = 5 To (nbrParams - 8)
        Debug.Print params(i)
    Next i    </pre>
<pre>End Sub</pre>

</body>

</html>
