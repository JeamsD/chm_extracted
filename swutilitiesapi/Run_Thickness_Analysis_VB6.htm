<!doctype HTML public "-//W3C//DTD HTML 4.0 Frameset//EN">

<html>

<!--(==============================================================)-->
<!--(Document created with RoboEditor. )============================-->
<!--(==============================================================)-->

<head>

<title>Run Thickness Analysis Example (VBA)</title>

<!--(Meta)==========================================================-->

<meta http-equiv=content-type content="text/html; charset=windows-1252">
<META NAME="MS-HKWD" CONTENT="VBA examples">
<meta NAME=MS-HKWD CONTENT="VBA examples, thickness analysis">
<meta name=generator content="RoboHELP by eHelp Corporation - www.ehelp.com">
<meta name=generator-major-version content=0.1>
<meta name=generator-minor-version content=1>
<meta name=filetype content=kadov>
<meta name=filetype-version content=1>
<meta name=page-count content=1>
<meta name=layout-height content=3557>
<meta name=layout-width content=937>


<!--(Links)=========================================================-->

<link rel="StyleSheet" href="./ApiHelp.css">



</head>

<!--(Body)==========================================================-->


<body>
<h1><span style="font-weight: normal; font-size: 7.5pt;">SOLIDWORKS Utilities API Help</span></h1>
<h1>Run Thickness Analysis Example (VBA)</h1>

<p>This example shows how to run a thickness analysis and generate reports 
 using the SOLIDWORKS Utilities API.</p>

<pre>'-----------------------------------------------------------------------------
' Preconditions:
' 1. Add the SOLIDWORKS Utilities as an add-in
'    (in SOLIDWORKS, click <b>Tools &gt; Add-Ins &gt; SOLIDWORKS Utilities</b>).
' 2. Add the SOLIDWORKS Utilities type library as a reference
'    (in the SOLIDWORKS Microsoft Visual Basic for Applications IDE, click
'    <b>Tools &gt; References &gt; SolidWorks Utilities</b> &lt;<i>version</i>&gt; <b>Type Library</b>).
' 3. Verify that the specified part exists.
' 4. Verify that <b>c:\test</b> exists.
'
' Postconditions:
' 1. Opens the part and runs a thin thickness analysis.
' 2. Adds the thickness analysis report to the Design Binder.
' 3. Saves the thickness analysis report to <b>c:\test\report</b>.
' 4. Examine the Immediate window, Design Binder, and <b>c:\test\report\gtReportIndex.htm</b>.
'
' NOTE: Because the part is used elsewhere, do not save changes.
'------------------------------------------------------------------------------
Option Explicit</pre>
<pre>Dim swApp As SldWorks.SldWorks</pre>
<pre>Sub main()</pre>
<pre>    Dim swModel As SldWorks.ModelDoc2
    Dim utAddIn As SWUtilities.gtcocswUtilities
    Dim utThicknessAnalysis As SWUtilities.gtcocswThicknessAnalysis
    Dim nOption As SWUtilities.gtResultOptions_e
    Dim nResolution As SWUtilities.gttckResolutionOptions_e
    Dim strReportName As String
    Dim lStatus As Long
    Dim bAddToBinder As Boolean
    Dim bSaveToEdwg As Boolean
    Dim bOverWrite As Boolean
    Dim vCriticalFeatureNames As Variant
    Dim lIdx As Long
    Dim dRangeMin As Double
    Dim dRangeMax As Double
    Dim lNumFaces As Long
    Dim dSurfArea As Double
    Dim dPerAnalysisArea As Double
    Dim lNumRanges As Long
    Dim dThicknessLimit As Double
    Dim lErrors As Long
    Dim lWarnings As Long
    Dim sPart As String</pre>
<pre>    ' Connect to SOLIDWORKS
    Set swApp = Application.SldWorks</pre>
<pre>    ' Load the document for analysis
    sPart = &quot;C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\introtosw\pressure_plate.sldprt&quot;
    Set swModel = swApp.<b>OpenDoc6</b>(sPart, swDocPART, swOpenDocOptions_Silent, &quot;&quot;, lErrors, lWarnings)</pre>
<pre>    ' Load the SOLIDWORKS Utilities add-in
    Set utAddIn = swApp.<b>GetAddInObject</b>(&quot;Utilities.UtilitiesApp&quot;)</pre>
<pre>    ' Get the thickness analysis tool
    Set utThicknessAnalysis = utAddIn.<b>ThicknessAnalysis</b></pre>
<pre>    ' Initialize the thickness analysis tool
    lStatus = utThicknessAnalysis.<b>Init</b>()
    If lStatus &lt;&gt; gtNOErr Then
        Debug.Print &quot;Thickness analysis tool could not be initialized: &quot; &amp; CStr(lStatus)
        Exit Sub
    End If</pre>
<pre>    ' Set the options
    ' Save the report
    nOption = gtResultSaveReport</pre>
<pre>    ' Use high resolution
    nResolution = gttckHighResolution</pre>
<pre>    ' Save the report to this folder
    strReportName = &quot;c:\test\report&quot;</pre>
<pre>    ' Add the report to the Design Binder
    bAddToBinder = True</pre>
<pre>    ' Do not save the report to eDrawings
    bSaveToEdwg = False</pre>
<pre>    ' Allow the report to be overwritten, both in Design Binder and
    ' on disk, so that you can rerun the analysis
    bOverWrite = True</pre>
<pre>    ' Set the thickness threshold
    dThicknessLimit = 0.025</pre>
<pre>    ' Run the analysis
    lStatus = utThicknessAnalysis.<b>RunThinAnalysis2</b>(dThicknessLimit, nResolution, nOption, strReportName, bAddToBinder, bSaveToEdwg, bOverWrite)
        </pre>
<pre>    ' Check the result
    If lStatus &lt;&gt; gtNOErr Then
        Debug.Print &quot;Thickness analysis completed an with error: &quot; &amp; CStr(lStatus)
        ' Close the thickness analysis tool
        lStatus = utThicknessAnalysis.<b>Close</b>()
        ' Release
        Set utThicknessAnalysis = Nothing
        Set utAddIn = Nothing
        Exit Sub
    End If</pre>
<pre>    ' Get results
    Debug.Print &quot;Total surface area analyzed              = &quot; &amp; CStr(utThicknessAnalysis.<b>GetTotalSurfaceAreaAnalyzed</b>(lStatus))
    Debug.Print &quot;Critical area                            = &quot; &amp; CStr(utThicknessAnalysis.<b>GetCriticalSurfaceArea</b>(lStatus))
    Debug.Print &quot;Maximum deviation                        = &quot; &amp; CStr(utThicknessAnalysis.<b>GetMaxDeviationfromTargetThickness</b>(lStatus))
    Debug.Print &quot;Average weighted thickness critical area = &quot; &amp; CStr(utThicknessAnalysis.<b>GetAvgWeightedTckOnCritArea</b>(lStatus))
    Debug.Print &quot;Average weighted thickness analyzed area = &quot; &amp; CStr(utThicknessAnalysis.<b>GetAvgWeightedTckOnAnalArea</b>(lStatus))
    Debug.Print &quot;Number of critical faces                 = &quot; &amp; CStr(utThicknessAnalysis.<b>GetNumCriticalFaces</b>(lStatus))
    Debug.Print &quot;Number of critical features              = &quot; &amp; CStr(utThicknessAnalysis.<b>GetNumCriticalFeatures</b>(lStatus))
    vCriticalFeatureNames = utThicknessAnalysis.<b>GetCriticalFeatureNames</b>(lStatus)
    If Not IsEmpty(vCriticalFeatureNames) Then
        Debug.Print &quot;Critical features:&quot;
        For lIdx = LBound(vCriticalFeatureNames) To UBound(vCriticalFeatureNames)
            Debug.Print &quot;  &quot; &amp; vCriticalFeatureNames(lIdx)
        Next lIdx
    End If</pre>
<pre>    Debug.Print &quot;Minimum thickness analyzed area          = &quot; &amp; CStr(utThicknessAnalysis.<b>GetMinTckOnAnalArea</b>(lStatus))
    Debug.Print &quot;Maximum thickness analyzed area          = &quot; &amp; CStr(utThicknessAnalysis.<b>GetMaxTckOnAnalArea</b>(lStatus))
    lNumRanges = utThicknessAnalysis.<b>GetIntervalCount</b>(lStatus)
    Debug.Print &quot;Number of intervals                      = &quot; &amp; lNumRanges; CStr(utThicknessAnalysis.GetIntervalCount(lStatus))
    For lIdx = 1 To lNumRanges
        Debug.Print &quot;  #&quot; &amp; CStr(lIdx)
        lStatus = utThicknessAnalysis.<b>GetAnalysisDetails</b>(lIdx, dRangeMin, dRangeMax, lNumFaces, dSurfArea, dPerAnalysisArea)
        If lStatus &lt;&gt; gtNOErr Then
            Debug.Print &quot;Could not obtain analysis details.&quot;
        Else
            Debug.Print &quot;     min            = &quot; &amp; CStr(dRangeMin)
            Debug.Print &quot;     max            = &quot; &amp; CStr(dRangeMax)
            Debug.Print &quot;     #faces         = &quot; &amp; CStr(lNumFaces)
            Debug.Print &quot;     surf area      = &quot; &amp; CStr(dSurfArea)
            Debug.Print &quot;     %analyzed area = &quot; &amp; CStr(dPerAnalysisArea)
        End If
    Next lIdx
    </pre>
<pre>    ' Close the thickness analysis tool
    lStatus = utThicknessAnalysis.<b>Close</b>()</pre>
<pre>    ' Release
    Set utThicknessAnalysis = Nothing
    Set utAddIn = Nothing</pre>
<pre>    ' Done
    Exit Sub</pre>
<pre>End Sub</pre>

</body>

</html>
